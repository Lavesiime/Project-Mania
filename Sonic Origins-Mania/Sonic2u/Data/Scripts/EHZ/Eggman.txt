// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Eggman Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Using EHZEggman as a prefix here because there's like 12 "Eggman" objects in the game so it's less confusing this way

// ========================
// Aliases
// ========================

private alias object.value0  : object.timer
private alias object.value2  : object.exploding
private alias object.value3  : object.heliAnim
private alias object.value4  : object.heliAnimTimer
private alias object.value6  : object.heliSfxTimer
private alias object.value8  : object.health
private alias object.value9  : object.invincibilityTimer
private alias object.value10 : object.didHitPlayer
private alias object.value11 : object.eggmanFrame
private alias object.value12 : object.mobileAnimTimer
private alias object.value13 : object.heliFrame

// Eggman States
private alias 0 : EHZEGGMAN_AWAITPLAYER
private alias 1 : EHZEGGMAN_FLYIN
private alias 2 : EHZEGGMAN_ENTERCAR
private alias 3 : EHZEGGMAN_STARTCAR
private alias 4 : EHZEGGMAN_INCAR
private alias 5 : EHZEGGMAN_EXPLODE
private alias 6 : EHZEGGMAN_EXITCAR
private alias 7 : EHZEGGMAN_FLEE
private alias 8 : EHZEGGMAN_ESCAPE

// Eggman Animations
private alias 0 : EHZEGGANI_INVISIBLE
private alias 1 : EHZEGGANI_IDLE
private alias 2 : EHZEGGANI_LAUGH
private alias 3 : EHZEGGANI_HIT
private alias 4 : EHZEGGANI_DEFEATED
private alias 5 : EHZEGGANI_TOASTED
private alias 6 : EHZEGGANI_PANIC

// Eggman Heli Animations
private alias 0 : HELIANI_ACTIVE
private alias 1 : HELIANI_ENTERCAR
private alias 2 : HELIANI_RETRACTING
private alias 3 : HELIANI_NONE
private alias 4 : HELIANI_EXTENDING
private alias 5 : HELIANI_EXITCAR

// Car Wheel Aliases
private alias object.value0 : eggmanWheel.timer
private alias object.value1 : eggmanWheel.xOffset
private alias object.value2 : eggmanWheel.parent

// Car Aliases
private alias object.value1 : eggmanCar.drawPos.y
private alias object.value2 : eggmanCar.boundsL
private alias object.value3 : eggmanCar.boundsR

// Car Drill States
private alias 0 : EHZEGGMANDRILL_ATTATCHED
private alias 2 : EHZEGGMANDRILL_FIRED

// Player Aliases
private alias object.xpos : player.xpos
private alias object.xvel : player.xvel
private alias object.speed : player.speed
private alias object.animation : player.animation
private alias object.collisionRight : player.collisionRight

private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom

// Music Events
private alias 26 : SLOT_MUSICEVENT_BOSS

private alias  0 : MUSICEVENT_FADETOBOSS
private alias  1 : MUSICEVENT_FADETOSTAGE


// ========================
// Function Declarations
// ========================

reserve function EHZEggman_PlayHeliSfx


// ========================
// Function Definitions
// ========================

private function EHZEggman_PlayHeliSfx
	if object.heliSfxTimer == 0
		PlaySfx(SfxName[Helicopter], false)
	end if
	object.heliSfxTimer++
	object.heliSfxTimer &= 31
end function


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case EHZEGGMAN_AWAITPLAYER
		temp0 = object.ixpos
		temp0 -= 200
		stage.newXBoundary1 = temp0

		temp0 = object.ixpos
		temp0 += 200
		stage.newXBoundary2 = temp0

		if player[0].xpos > object.xpos
			object.animation = EHZEGGANI_IDLE
			ResetObjectEntity(SLOT_MUSICEVENT_BOSS, TypeName[Music Event], MUSICEVENT_FADETOBOSS, 0, 0)
			object[SLOT_MUSICEVENT_BOSS].priority = PRIORITY_ACTIVE

			// The Car!!!!
			object[+1].type = TypeName[Eggman Car]
			object[+1].xpos = object.xpos
			object[+1].xpos += 0x1590000
			object[+1].ypos = object.ypos
			eggmanCar[+1].boundsL = object.xpos
			eggmanCar[+1].boundsL -= 0x1180000
			temp0 = object.ixpos
			temp0 -= 200
			temp0 += screen.xcenter
			if camera[0].xpos < temp0
				temp0 -= camera[0].xpos
				temp0 <<= 16
				eggmanCar[+1].boundsL -= temp0
			end if

			eggmanCar[+1].boundsR = object.xpos
			eggmanCar[+1].boundsR += 0x1500000
			object[+1].priority = PRIORITY_ACTIVE

			// Drrrrrill
			object[+2].type = TypeName[Eggman Drill]
			object[+2].priority = PRIORITY_ACTIVE

			// Back Wheel
			object[-1].type = TypeName[Eggman Wheel]
			eggmanWheel[-1].xOffset = -0x2C0000
			eggmanWheel[-1].parent = object[+1].entityPos
			object[-1].priority = PRIORITY_ACTIVE

			// Front Wheel 1
			object[+3].type = TypeName[Eggman Wheel]
			eggmanWheel[+3].xOffset = -0xC0000
			eggmanWheel[+3].parent = object[+1].entityPos
			object[+3].priority = PRIORITY_ACTIVE

			// Front Wheel 2
			object[+4].type = TypeName[Eggman Wheel]
			eggmanWheel[+4].xOffset = 0x1C0000
			eggmanWheel[+4].parent = object[+1].entityPos
			object[+4].priority = PRIORITY_ACTIVE

			object.xpos += 0x1580000
			object.ypos -= 0x13F0000
			object.priority = PRIORITY_ACTIVE
			object.state++
		end if
		break

	case EHZEGGMAN_FLYIN
		CallFunction(EHZEggman_PlayHeliSfx)
		object.xpos -= 0x10000
		object.ypos += 0x10000
		object.timer++
		if object.timer == 320
			object.heliAnim = HELIANI_ENTERCAR
			object.heliAnimTimer = 0
			object.timer = 0
			object.state++
		end if
		break

	case EHZEGGMAN_ENTERCAR
		break
		
	case EHZEGGMAN_STARTCAR
		object.timer++
		if object.timer == 24
			object.timer = 0
			
#platform: USE_STANDALONE
			object.health = 8
#endplatform
#platform: USE_ORIGINS
			if game.bossOneLife == false
				object.health = 8
			else
				object.health = 1
			end if
#endplatform

			object.state++
			object[+1].xvel = -0x20000
			object[+1].state++
		end if
		break

	case EHZEGGMAN_INCAR
		break

	case EHZEGGMAN_EXPLODE
		object.timer++
		if object.timer == 180
			object.timer = 0
			object.heliAnim = HELIANI_EXTENDING
			object.animation = EHZEGGANI_TOASTED
			object.exploding = false
			object.state++
			ResetObjectEntity(SLOT_MUSICEVENT_BOSS, TypeName[Music Event], MUSICEVENT_FADETOSTAGE, 0, 0)
			object[SLOT_MUSICEVENT_BOSS].priority = PRIORITY_ACTIVE
			temp0 = tileLayer[0].xsize
			temp0 <<= 7
			stage.newXBoundary2 = temp0
		end if
		break

	case EHZEGGMAN_EXITCAR
		break

	case EHZEGGMAN_FLEE
		CallFunction(EHZEggman_PlayHeliSfx)
		object.ypos -= 0x10000

		object.timer++
		if object.timer == 96
			object.timer = 0
			object.direction = FLIP_X
			object.animation = EHZEGGANI_PANIC
			object.state++
		end if
		break

	case EHZEGGMAN_ESCAPE
		if object.outOfBounds == false
			CallFunction(EHZEggman_PlayHeliSfx)
		end if
		object.xpos += 0x60000

		temp0 = stage.newXBoundary2
		temp0 += 128
		temp0 <<= 16
		if object.xpos >= temp0
			object.type = TypeName[Blank Object]
		end if
		break
		
	end switch

	if object.health != 0
		if object.invincibilityTimer > 0
			object.invincibilityTimer--
			GetBit(temp0, object.invincibilityTimer, 0)
			if temp0 == true // Palette flashing when hit
				SetPaletteEntry(0, 192, 0xE0E0E0)
			else
				SetPaletteEntry(0, 192, 0x000000)
			end if
		end if

		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			if object.invincibilityTimer == 0 // Invulnerable?
				CheckEqual(player[currentPlayer].animation, ANI_HURT)
				temp0 = checkResult
				CheckEqual(player[currentPlayer].animation, ANI_DYING)
				temp0 |= checkResult
				CheckEqual(player[currentPlayer].animation, ANI_DROWNING)
				temp0 |= checkResult
				if temp0 != false
					if object.animation != EHZEGGANI_LAUGH
						object.animation = EHZEGGANI_LAUGH
						object.animationTimer = 0
					end if

					if currentPlayer == 0
						object.didHitPlayer = true
					end if
				end if

				BoxCollisionTest(C_TOUCH, object.entityPos, -24, -24, 24, 24, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
				if checkResult == true
					CallFunction(Player_CheckHit)
					if checkResult == true
						object.health--
						if object.health == 0
#platform: USE_ORIGINS
							CallNativeFunction2(NotifyCallback, NOTIFY_KILL_BOSS, 0)
							CallNativeFunction2(NotifyCallback, NOTIFY_BOSS_END, 1)
#endplatform
							
							player.score += 1000
							object.animation = EHZEGGANI_DEFEATED
							object.animationTimer = 0
							object.exploding = true
							object.state++

							// The Car!!!!
							object[+1].state++
							object[+1].yvel = -0x18000
							object[+1].ypos = eggmanCar[+1].drawPos.y

							if object[+2].state == EHZEGGMANDRILL_ATTATCHED
								object[+2].state = EHZEGGMANDRILL_FIRED
								if object.direction == FLIP_NONE
									object[+2].xvel = -0x30000
								else
									object[+2].xvel = 0x30000
								end if
							end if
						else
							object.animation = EHZEGGANI_HIT
							object.animationTimer = 0
							object.invincibilityTimer = 32
							PlaySfx(SfxName[Boss Hit], false)
						end if
					end if
				end if
			end if
		next
	end if

	// R Boundary
	if object.state != EHZEGGMAN_ESCAPE
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			temp0 = player[currentPlayer].collisionRight
			temp0 <<= 16
			temp0 += player[currentPlayer].xpos
			temp1 = stage.curXBoundary2
			temp1 <<= 16
			if temp0 > temp1
				player[currentPlayer].xvel = 0
				player[currentPlayer].speed = 0
				player[currentPlayer].xpos = temp1
				temp0 = player[currentPlayer].collisionRight
				temp0 <<= 16
				player[currentPlayer].xpos -= temp0
			end if
		next
	end if

	if object.exploding == true
		temp0 = oscillation
		temp0 &= 7
		if temp0 == 0
			Rand(temp0, 96)
			temp0 -= 48
			temp0 <<= 16
			temp0 += object.xpos

			Rand(temp1, 48)
			temp1 -= 24
			temp1 <<= 16
			temp1 += object.ypos

			CreateTempObject(TypeName[Explosion], 0, temp0, temp1)
			object[tempObjectPos].drawOrder = 5
			PlaySfx(SfxName[Explosion], false)
		end if
	end if
end event


event ObjectDraw
	if object.animation != EHZEGGANI_INVISIBLE
		switch object.heliAnim
		case HELIANI_ACTIVE
			if object.heliFrame < 194
				object.heliFrame = 194
			end if
			object.heliAnimTimer++
			object.heliFrame++
			if object.heliFrame > 206
				object.heliFrame = 194
			end if
			DrawSpriteFX(object.heliFrame, FX_FLIP, object.xpos, object.ypos)
			break

		case HELIANI_ENTERCAR
			temp0 = object.heliAnimTimer
			temp0 /= 6
			temp0 += 194

			object.heliAnimTimer++
			if object.heliAnimTimer == 24
				object.heliAnimTimer = 0
				object.heliAnim = HELIANI_RETRACTING
			end if

			DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos)
			break

		case HELIANI_RETRACTING
			temp0 = object.heliAnimTimer
			temp0 <<= 16
			temp0 += object.ypos
			DrawSpriteFX(194, FX_FLIP, object.xpos, temp0)

			object.heliAnimTimer++
			if object.heliAnimTimer == 12
				object.state++
				object.heliAnim = HELIANI_NONE
			end if
			break

		case HELIANI_NONE
			break

		case HELIANI_EXTENDING
			object.heliAnimTimer--
			if object.heliAnimTimer > 0
				temp0 = object.heliAnimTimer
			else
				temp0 = 0
			end if

			temp0 <<= 16
			temp0 += object.ypos
			DrawSpriteFX(194, FX_FLIP, object.xpos, temp0)

			if object.heliAnimTimer == -12
				object.heliAnimTimer = 24
				object.heliAnim = HELIANI_EXITCAR
			end if
			break

		case HELIANI_EXITCAR
			object.heliAnimTimer--

			temp0 = object.heliAnimTimer
			temp0 /= 6
			temp0 += 194

			if object.heliAnimTimer == 0
				object.heliAnim = HELIANI_ACTIVE
				object.state++
			end if

			DrawSpriteFX(temp0, FX_FLIP, object.xpos, object.ypos)
			break
			
		end switch

		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
	end if
	
	switch object.animation
	default
	case EHZEGGANI_INVISIBLE
		break

	case EHZEGGANI_IDLE
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 3
			object.eggmanFrame = 3
		end if
		if object.eggmanFrame > 11
			object.eggmanFrame = 3
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		break

	case EHZEGGANI_LAUGH
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 12
			object.eggmanFrame = 12
		end if
		if object.eggmanFrame > 108
			object.animation = EHZEGGANI_IDLE
		end if
		if object.eggmanFrame > 107
			if object[0].animation != ANI_DYING
				object.animation = EHZEGGANI_IDLE
			end if
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		break

	case EHZEGGANI_HIT
		object.animationTimer++
		object.eggmanFrame++
		object.mobileAnimTimer++
		if object.mobileAnimTimer > 1
			object.frame++
			object.mobileAnimTimer = 0
		end if
		if object.frame < 0
			object.frame = 0
		end if
		if object.frame > 1
			object.frame = 0
		end if
		if object.eggmanFrame < 109
			object.eggmanFrame = 109
		end if
		if object.eggmanFrame > 139
			object.eggmanFrame = 109
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.frame, FX_FLIP, object.xpos, object.ypos)
		if object.animationTimer == 30
			object.mobileAnimTimer = 0
			object.animationTimer = 0
			object.animation = EHZEGGANI_IDLE
		end if
		break

	case EHZEGGANI_DEFEATED
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 140
			object.eggmanFrame = 140
		end if
		if object.eggmanFrame > 175
			object.eggmanFrame = 140
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		break

	case EHZEGGANI_TOASTED
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 140
			object.eggmanFrame = 140
		end if
		if object.eggmanFrame > 175
			object.eggmanFrame = 140
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		break

	case EHZEGGANI_PANIC
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 176
			object.eggmanFrame = 176
		end if
		if object.eggmanFrame > 193
			object.eggmanFrame = 176
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		break
		
	end switch
end event


event ObjectStartup
	// Check if in EHZ
	CheckCurrentStageFolder("Zone01")
	if checkResult == true
		// Load EHZ stuff
		switch stage.playerListPos
		case PLAYER_SONIC_A
			LoadSpriteSheet("Global/Eggman.gif")
			break

		case PLAYER_TAILS_A
			LoadSpriteSheet("Global/Eggman.gif")
			break

		case PLAYER_KNUCKLES_A
			LoadSpriteSheet("Global/EggRobo.gif")
			break
#platform: USE_ORIGINS
		case PLAYER_AMY_A
			LoadSpriteSheet("Global/Eggman.gif")
			break
#endplatform
		end switch

		// Empty Egg Mobile
		SpriteFrame(-32, -19, 64, 40, 83, 97) // Frame 0
		SpriteFrame(-32, -19, 64, 40, 83, 97) // Frame 1

		// Egg Mobile Seat
		SpriteFrame(-5, -28, 38, 20, 83, 138) // Frame 2
		
		// Eggman Idle
		SpriteFrame(-25, -28, 40, 23, 1, 1) // Frame 3
		SpriteFrame(-25, -28, 40, 23, 1, 1) // Frame 4
		SpriteFrame(-25, -28, 40, 23, 1, 1) // Frame 5
		SpriteFrame(-25, -28, 40, 23, 1, 25) // Frame 6
		SpriteFrame(-25, -28, 40, 23, 1, 25) // Frame 7
		SpriteFrame(-25, -28, 40, 23, 1, 25) // Frame 8
		SpriteFrame(-25, -28, 40, 23, 1, 49) // Frame 9
		SpriteFrame(-25, -28, 40, 23, 1, 49) // Frame 10
		SpriteFrame(-25, -28, 40, 23, 1, 49) // Frame 11

		//Eggman Laugh
		SpriteFrame(-25, -28, 40, 23, 1, 1) // Frame 12
		SpriteFrame(-25, -28, 40, 23, 1, 1) // Frame 13

		SpriteFrame(-25, -28, 40, 23, 1, 73) // Frame 14
		SpriteFrame(-25, -28, 40, 23, 1, 73) // Frame 15
		SpriteFrame(-25, -28, 40, 23, 1, 73) // Frame 16
		SpriteFrame(-25, -28, 40, 23, 1, 73) // Frame 17

		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 18
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 19
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 20
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 21

		SpriteFrame(-25, -28, 40, 23, 1, 145) // Frame 22
		SpriteFrame(-25, -28, 40, 23, 1, 145) // Frame 23
		SpriteFrame(-25, -28, 40, 23, 1, 145) // Frame 24
		SpriteFrame(-25, -28, 40, 23, 1, 145) // Frame 25

		SpriteFrame(-25, -28, 40, 23, 1, 121) // Frame 26
		SpriteFrame(-25, -28, 40, 23, 1, 121) // Frame 27
		SpriteFrame(-25, -28, 40, 23, 1, 121) // Frame 28
		SpriteFrame(-25, -28, 40, 23, 1, 121) // Frame 29

		SpriteFrame(-25, -28, 40, 23, 1, 145) // Frame 30
		SpriteFrame(-25, -28, 40, 23, 1, 145) // Frame 31
		SpriteFrame(-25, -28, 40, 23, 1, 145) // Frame 32
		SpriteFrame(-25, -28, 40, 23, 1, 145) // Frame 33

		SpriteFrame(-25, -28, 40, 23, 1, 121) // Frame 34
		SpriteFrame(-25, -28, 40, 23, 1, 121) // Frame 35
		SpriteFrame(-25, -28, 40, 23, 1, 121) // Frame 36
		SpriteFrame(-25, -28, 40, 23, 1, 121) // Frame 37

		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 38
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 39
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 40
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 41
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 42

		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 43
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 44
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 45
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 46
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 47

		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 48
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 49
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 50
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 51
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 52

		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 53
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 54
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 55
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 56
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 57

		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 58
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 59
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 60
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 61
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 62

		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 63
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 64
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 65
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 66
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 67

		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 68
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 69
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 70
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 71
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 72

		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 73
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 74
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 75
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 76
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 77

		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 78
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 79
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 80
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 81
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 82

		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 83
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 84
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 85
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 86
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 87

		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 88
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 89
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 90
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 91
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 92

		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 93
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 94
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 95
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 96
		SpriteFrame(-25, -28, 40, 23, 1, 97) // Frame 97

		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 98
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 99
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 10
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 101
		SpriteFrame(-25, -28, 40, 23, 1, 169) // Frame 102

		SpriteFrame(-25, -28, 40, 23, 1, 73) // Frame 103
		SpriteFrame(-25, -28, 40, 23, 1, 73) // Frame 104
		SpriteFrame(-25, -28, 40, 23, 1, 73) // Frame 105
		SpriteFrame(-25, -28, 40, 23, 1, 73) // Frame 106

		SpriteFrame(-25, -28, 40, 23, 1, 1) // Frame 107
		SpriteFrame(-25, -28, 40, 23, 1, 1) // Frame 108

		// Eggman Hurt
		SpriteFrame(-25, -28, 40, 23, 1, 193) // Frame 109
		SpriteFrame(-25, -28, 40, 23, 1, 193) // Frame 110

		SpriteFrame(-25, -28, 40, 23, 1, 217) // Frame 111
		SpriteFrame(-25, -28, 40, 23, 1, 217) // Frame 112

		SpriteFrame(-25, -28, 40, 23, 42, 1) // Frame 113
		SpriteFrame(-25, -28, 40, 23, 42, 1) // Frame 114

		SpriteFrame(-25, -28, 40, 23, 1, 217) // Frame 115
		SpriteFrame(-25, -28, 40, 23, 1, 217) // Frame 116

		SpriteFrame(-25, -28, 40, 23, 42, 25) // Frame 117
		SpriteFrame(-25, -28, 40, 23, 42, 25) // Frame 118

		SpriteFrame(-25, -28, 40, 23, 1, 217) // Frame 119
		SpriteFrame(-25, -28, 40, 23, 1, 217) // Frame 120

		SpriteFrame(-25, -28, 40, 23, 42, 49) // Frame 121
		SpriteFrame(-25, -28, 40, 23, 42, 49) // Frame 122

		SpriteFrame(-25, -28, 40, 23, 1, 217) // Frame 123
		SpriteFrame(-25, -28, 40, 23, 1, 217) // Frame 124
		SpriteFrame(-25, -28, 40, 23, 1, 217) // Frame 125

		SpriteFrame(-25, -28, 40, 23, 42, 73) // Frame 126
		SpriteFrame(-25, -28, 40, 23, 42, 73) // Frame 127
		SpriteFrame(-25, -28, 40, 23, 42, 73) // Frame 128

		SpriteFrame(-25, -28, 40, 23, 1, 217) // Frame 129
		SpriteFrame(-25, -28, 40, 23, 1, 217) // Frame 130
		SpriteFrame(-25, -28, 40, 23, 1, 217) // Frame 131

		SpriteFrame(-25, -28, 40, 23, 42, 97) // Frame 132
		SpriteFrame(-25, -28, 40, 23, 42, 97) // Frame 133
		SpriteFrame(-25, -28, 40, 23, 42, 97) // Frame 134
		SpriteFrame(-25, -28, 40, 23, 42, 97) // Frame 135
		SpriteFrame(-25, -28, 40, 23, 42, 97) // Frame 136
		SpriteFrame(-25, -28, 40, 23, 42, 97) // Frame 137

		SpriteFrame(-25, -28, 40, 23, 1, 1) // Frame 138
		SpriteFrame(-25, -28, 40, 23, 1, 1) // Frame 139

		// Eggman Toasted
		SpriteFrame(-25, -28, 40, 23, 42, 121) // Frame 140
		SpriteFrame(-25, -28, 40, 23, 42, 121) // Frame 141
		SpriteFrame(-25, -28, 40, 23, 42, 121) // Frame 142
		SpriteFrame(-25, -28, 40, 23, 42, 121) // Frame 143
		SpriteFrame(-25, -28, 40, 23, 42, 121) // Frame 144
		SpriteFrame(-25, -28, 40, 23, 42, 121) // Frame 145
		SpriteFrame(-25, -28, 40, 23, 42, 145) // Frame 146
		SpriteFrame(-25, -28, 40, 23, 42, 145) // Frame 147
		SpriteFrame(-25, -28, 40, 23, 42, 145) // Frame 148
		SpriteFrame(-25, -28, 40, 23, 42, 145) // Frame 149
		SpriteFrame(-25, -28, 40, 23, 42, 145) // Frame 150
		SpriteFrame(-25, -28, 40, 23, 42, 145) // Frame 151
		SpriteFrame(-25, -28, 40, 23, 42, 169) // Frame 152
		SpriteFrame(-25, -28, 40, 23, 42, 169) // Frame 153
		SpriteFrame(-25, -28, 40, 23, 42, 169) // Frame 154
		SpriteFrame(-25, -28, 40, 23, 42, 169) // Frame 155
		SpriteFrame(-25, -28, 40, 23, 42, 169) // Frame 156
		SpriteFrame(-25, -28, 40, 23, 42, 169) // Frame 157
		SpriteFrame(-25, -28, 40, 23, 42, 193) // Frame 158
		SpriteFrame(-25, -28, 40, 23, 42, 193) // Frame 159
		SpriteFrame(-25, -28, 40, 23, 42, 193) // Frame 160
		SpriteFrame(-25, -28, 40, 23, 42, 193) // Frame 161
		SpriteFrame(-25, -28, 40, 23, 42, 193) // Frame 162
		SpriteFrame(-25, -28, 40, 23, 42, 193) // Frame 163
		SpriteFrame(-25, -28, 40, 23, 42, 217) // Frame 164
		SpriteFrame(-25, -28, 40, 23, 42, 217) // Frame 165
		SpriteFrame(-25, -28, 40, 23, 42, 217) // Frame 166
		SpriteFrame(-25, -28, 40, 23, 42, 217) // Frame 167
		SpriteFrame(-25, -28, 40, 23, 42, 217) // Frame 168
		SpriteFrame(-25, -28, 40, 23, 42, 217) // Frame 169
		SpriteFrame(-25, -28, 40, 23, 83, 1) // Frame 170
		SpriteFrame(-25, -28, 40, 23, 83, 1) // Frame 171
		SpriteFrame(-25, -28, 40, 23, 83, 1) // Frame 172
		SpriteFrame(-25, -28, 40, 23, 83, 1) // Frame 173
		SpriteFrame(-25, -28, 40, 23, 83, 1) // Frame 174
		SpriteFrame(-25, -28, 40, 23, 83, 1) // Frame 175

		// Eggman Sweat
		SpriteFrame(-25, -28, 40, 23, 83, 25) // Frame 176
		SpriteFrame(-25, -28, 40, 23, 83, 25) // Frame 177
		SpriteFrame(-25, -28, 40, 23, 83, 25) // Frame 178
		SpriteFrame(-25, -28, 40, 23, 83, 49) // Frame 179
		SpriteFrame(-25, -28, 40, 23, 83, 49) // Frame 180
		SpriteFrame(-25, -28, 40, 23, 83, 49) // Frame 181
		SpriteFrame(-25, -28, 40, 23, 83, 73) // Frame 182
		SpriteFrame(-25, -28, 40, 23, 83, 73) // Frame 183
		SpriteFrame(-25, -28, 40, 23, 83, 73) // Frame 184
		SpriteFrame(-25, -28, 40, 23, 1, 1) // Frame 185
		SpriteFrame(-25, -28, 40, 23, 1, 1) // Frame 186
		SpriteFrame(-25, -28, 40, 23, 1, 1) // Frame 187
		SpriteFrame(-25, -28, 40, 23, 1, 25) // Frame 188
		SpriteFrame(-25, -28, 40, 23, 1, 25) // Frame 189
		SpriteFrame(-25, -28, 40, 23, 1, 25) // Frame 190
		SpriteFrame(-25, -28, 40, 23, 1, 49) // Frame 191
		SpriteFrame(-25, -28, 40, 23, 1, 49) // Frame 192
		SpriteFrame(-25, -28, 40, 23, 1, 49) // Frame 193
		
		// Boss Helicopter Frames
		SpriteFrame(2, -40, 16, 16, 221, 1) // Frame 194
		SpriteFrame(-14, -40, 48, 16, 238, 1) // Frame 195
		SpriteFrame(-30, -40, 78, 16, 221, 17) // Frame 196
		SpriteFrame(-29, -40, 78, 16, 432, 1) // Frame 197
		SpriteFrame(-61, -40, 142, 16, 256, 35) // Frame 198
		SpriteFrame(-46, -40, 112, 16, 302, 18) // Frame 199
		SpriteFrame(-62, -40, 144, 16, 287, 1) // Frame 200
		SpriteFrame(-61, -40, 142, 16, 256, 35) // Frame 201
		SpriteFrame(-62, -40, 144, 16, 287, 1) // Frame 202
		SpriteFrame(-46, -40, 112, 16, 302, 18) // Frame 203
		SpriteFrame(-29, -40, 78, 16, 432, 1) // Frame 204
		SpriteFrame(-30, -40, 78, 16, 221, 17) // Frame 205
		SpriteFrame(-61, -40, 142, 16, 256, 35) // Frame 206

		// Erase the boss in VS mode
		if options.vsMode == true
			foreach (TypeName[Eggman], arrayPos0, ALL_ENTITIES)
				object[arrayPos0].type = TypeName[Blank Object]
			next
		end if
	else
		LoadSpriteSheet("MBZ/Objects.gif")
		
		// Eggman Frames
		SpriteFrame(-28, -28, 60, 20, 1, 1)
		SpriteFrame(-28, -28, 60, 20, 62, 1)
		SpriteFrame(-28, -28, 60, 20, 1, 22)
		SpriteFrame(-28, -28, 60, 20, 62, 22)
		SpriteFrame(-28, -28, 60, 20, 1, 43)
		SpriteFrame(-28, -28, 60, 20, 62, 43)
		
		// Eggmobile Body Frame
		SpriteFrame(-32, -8, 64, 29, 415, 170)
		
		// Boss Helicopter Frames
		SpriteFrame(2, -40, 16, 16, 169, 91)
		SpriteFrame(-62, -40, 144, 16, 105, 91)
		SpriteFrame(-46, -40, 112, 16, 121, 91)
		SpriteFrame(-30, -40, 80, 16, 137, 91)
		SpriteFrame(-14, -40, 48, 16, 153, 91)
		SpriteFrame(2, -40, 80, 16, 42, 64)
		SpriteFrame(-62, -40, 79, 16, 105, 91)
		
		// Sweat Frame
		SpriteFrame(-3, -28, 11, 8, 45, 88)

		// No need to erase the boss in VS mode when in MBZ since it should never even be active there anyway
	end if
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
	DrawSprite(1)
	
	if editor.showGizmos == true
		editor.drawingOverlay = true
		
		// Draw lines connecting the whole network of boss objects
		DrawArrow(object.xpos, object.ypos, object[-1].xpos, object[-1].ypos, 255, 255, 0) // Back Wheel
		DrawArrow(object.xpos, object.ypos, object[+1].xpos, object[+1].ypos, 255, 255, 0) // Car
		DrawArrow(object.xpos, object.ypos, object[+2].xpos, object[+2].ypos, 255, 255, 0) // Drill
		DrawArrow(object.xpos, object.ypos, object[+3].xpos, object[+3].ypos, 255, 255, 0) // Front Wheel 1
		DrawArrow(object.xpos, object.ypos, object[+4].xpos, object[+4].ypos, 255, 255, 0) // Front Wheel 2
		
		// Draw arena bounds
		// Left bounds depends on screen size, this is a rough estimation
		temp0 = 224; temp1 = 208; temp2 = 200; temp3 = 32;
		CallFunction(EditorHelpers_DrawHitbox)
		
		editor.drawingOverlay = false
	end if
end event


event RSDKLoad
	CheckCurrentStageFolder("Zone01")
	if checkResult == true
		LoadSpriteSheet("EHZ/Objects.gif")
		SpriteFrame(-28, -28, 60, 20, 70, 155)
		SpriteFrame(-32, -8, 64, 29, 0, 209)
	else
		LoadSpriteSheet("MBZ/Objects.gif")
		SpriteFrame(-28, -28, 60, 20, 1, 1)
		SpriteFrame(-32, -8, 64, 29, 415, 170)
	end if
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
