// ----------------------------------
// RSDK Project: Sonic 2
// Script Description: Sign Post Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

// Variables
private alias object.value0 : object.fallbackTimer // Added in Origins 2.01
private alias object.value1 : object.spinCount
private alias object.value2 : object.spinTimer
private alias object.value3 : object.activePlayers
private alias object.value4 : object.allPlayersFinished
private alias object.value5 : object.lastPlayerID
private alias object.value6 : object.timer

// States
private alias 0 : SIGNPOST_IDLE
private alias 1 : SIGNPOST_SPUN
private alias 2 : SIGNPOST_EXITPLAYER
private alias 3 : SIGNPOST_IDLE_VS
private alias 4 : SIGNPOST_SPUN_VS
private alias 5 : SIGNPOST_UNUSED // used for SBZ2>3 transition in S1, not used in this game
private alias 6 : SIGNPOST_FINISHED
private alias 7 : SIGNPOST_WINK

// Player Aliases
private alias object.type 			: player.type
private alias object.propertyValue 	: player.character
private alias object.xpos 			: player.xpos
private alias object.ypos 			: player.ypos
private alias object.ixpos			: player.ixpos
private alias object.speed			: player.speed
private alias object.xvel 			: player.xvel
private alias object.yvel 			: player.yvel
private alias object.gravity 		: player.gravity
private alias object.animation		: player.animation
private alias object.controlMode 	: player.controlMode
private alias object.scrollTracking : player.scrollTracking
private alias object.interaction 	: player.interaction
private alias object.jumpPress 		: player.jumpPress
private alias object.jumpHold 		: player.jumpHold
private alias object.up 			: player.up
private alias object.down 			: player.down
private alias object.left 			: player.left
private alias object.right 			: player.right
private alias object.value7 		: player.invincibleTimer
private alias object.value21 		: player.acceleration
private alias object.value30		: player.jumpOffset

private alias 1 : SUPERSTATE_SUPER
private alias 2 : SUPERSTATE_FADEOUT

// VSGame aliases
private alias object.value0 : vsGame.timer
private alias object.value5 : vsGame.countdownPlayer

// VSGame States
private alias 0 : VSGAME_DISPLAYTAGS
private alias 2 : VSGAME_FADEOUT
private alias 3 : VSGAME_FINISH_COUNTDOWN
private alias 4 : VSGAME_GOTOVSRESULTS

// Game Modes
private alias 2 : MODE_TIMEATTACK

// Tracks
private alias 1 : TRACK_ACTFINISH

// Achievement Aliases
private alias  0 : ACHIEVEMENT_QUICKRUN
private alias  3 : ACHIEVEMENT_SUPERSTAR
private alias 10 : ACHIEVEMENT_SCRAMBLEDEGG

// Face Aliases
private alias 0 : FACE_TEXTURED_3D
private alias 1 : FACE_TEXTURED_2D
private alias 2 : FACE_COLOURED_3D
private alias 3 : FACE_COLOURED_2D
private alias 4 : FACE_FADED
private alias 5 : FACE_TEXTURED_C
private alias 6 : FACE_TEXTURED_D
private alias 7 : FACE_SPRITE3D

private alias object.value11: object.XCenter
private alias object.value12: object.YCenter
private alias object.value13: object.ZCenter

private alias object.value14: object.rectAlpha
private alias object.value15: object.debugToggle
private alias object.value16: object.cartAlpha
private alias object.value17: object.ZLock

private alias object.value18: object.logoXPos
private alias object.value19: object.boxTopTex
private alias object.value20: object.boxBotTex

private alias object.value21: object.XVectorPoint
private alias object.value22: object.YVectorPoint
private alias object.value23: object.ZVectorPoint

private alias object.value24: object.pointerX
private alias object.value25: object.pointerY
private alias object.value26: object.pointerZ
private alias object.value27: object.pointerXInc
private alias object.value28: object.pointerYInc
private alias object.value30: object.pointerZInc
private alias object.value31: object.invertRot
private alias object.value32: object.zpos

private alias object.value33 : object.pointX
private alias object.value34 : object.pointY
private alias object.value35 : object.pointZ

private alias object.value36 : object.xpos3D

// Function declarations
reserve function MainCover_GetBoxCenter

reserve function MainCover_DrawTopBotFaces
reserve function MainCover_DrawBackFace
reserve function MainCover_DrawFrontFace
reserve function MainCover_DrawLeftFace
reserve function MainCover_DrawRightFace

// Static Values

// Tables
private table MainCover_VectorPoint
	0, 0, -80
	10, 0, -80
	10, 58, -80
	0, 58, -80

	0, 0, 80
	10, 0, 80
	10, 58, 80
	0, 58, 80
end table


private table MainCover_VectorX[8]
private table MainCover_VectorY[8]
private table MainCover_VectorZ[8]


// ========================
// Function Declarations
// ========================

reserve function SignPost_DebugDraw
reserve function SignPost_DebugSpawn


// ========================
// Static Values
// ========================

public value SignPost_initFallback = 0
public value SignPost_fallbackCheck = 0


// ========================
// Function Definitions
// ========================

private function SignPost_DebugDraw
	DrawSprite(0)
	DrawSprite(1)
end function


private function SignPost_DebugSpawn
	CreateTempObject(TypeName[Sign Post], 0, object.xpos, object.ypos)
	object[tempObjectPos].frame = 1
	object[tempObjectPos].spinCount = 16
	if options.vsMode == true
		object[tempObjectPos].state = SIGNPOST_IDLE_VS
	end if
end function

private function MainCover_GetBoxCenter
	// Get the center of the object
	object.XCenter = 0
	object.YCenter = 0
	object.ZCenter = 0
	
	ArrayPos0 = 0
	while ArrayPos0 < 8
		temp0  = arrayPos0
		temp0 *= 3
		GetTableValue(object.XVectorPoint, temp0, MainCover_VectorPoint)
		temp0++
		GetTableValue(object.YVectorPoint, temp0, MainCover_VectorPoint)
		temp0++
		GetTableValue(object.ZVectorPoint, temp0, MainCover_VectorPoint)
		object.XCenter += object.XVectorPoint
		object.YCenter += object.YVectorPoint
		object.ZCenter += object.ZVectorPoint
		ArrayPos0++
	loop
	object.XCenter /= 8
	object.YCenter /= 8
	object.ZCenter /= 8
end function

private function MainCover_GetRotation
	arrayPos0 = 0
	while arrayPos0 < 8
		temp0  = arrayPos0
		temp0 *= 3
		GetTableValue(object.XVectorPoint, temp0, MainCover_VectorPoint)
		temp0++
		GetTableValue(object.YVectorPoint, temp0, MainCover_VectorPoint)
		temp0++
		GetTableValue(object.ZVectorPoint, temp0, MainCover_VectorPoint)
		object.pointX = object.XVectorPoint
		object.pointY = object.YVectorPoint
		object.pointZ = object.ZVectorPoint

		// Rotate X
		// X
		temp0   = object.pointX
		temp0  -= object.XCenter
		cos(object.angle, object.pointerZ)
		temp0  *= object.angle

		temp1   = object.pointY
		temp1  -= object.YCenter
		sin(object.angle, object.pointerZ)
		temp1  *= object.angle
		object.XVectorPoint  = temp0
		object.XVectorPoint -= temp1

		// Y
		temp0   = object.pointX
		temp0  -= object.XCenter
		Sin(object.angle, object.pointerZ)
		temp0  *= object.angle

		temp1   = object.pointY
		temp1  -= object.YCenter
		Cos(object.angle, object.pointerZ)
		temp1  *= object.angle
		object.YVectorPoint  = temp0
		object.YVectorPoint += temp1


		object.XVectorPoint += object.XCenter
		object.YVectorPoint += object.YCenter
		//object.ZVectorPoint += object.ZCenter
		object.XVectorPoint >>= 8
		object.YVectorPoint >>= 8
		//object.ZVectorPoint >>= 8
		SetTableValue(object.XVectorPoint, arrayPos0, MainCover_VectorX)
		SetTableValue(object.YVectorPoint, arrayPos0, MainCover_VectorY)
		//SetTableValue(object.ZVectorPoint, arrayPos0, MainCover_VectorZ)
		
		ArrayPos0++
	loop
end function

private function MainCover_DrawTopBotFaces
	arrayPos0 = 0
	while arrayPos0 < 8
		temp0  = arrayPos0
		temp0 *= 3
		GetTableValue(object.XVectorPoint, temp0, MainCover_VectorPoint)
		temp0++
		GetTableValue(object.YVectorPoint, temp0, MainCover_VectorPoint)
		temp0++
		GetTableValue(object.ZVectorPoint, temp0, MainCover_VectorPoint)
		VertexBuffer[arrayPos0].x = object.XCenter
		VertexBuffer[arrayPos0].y = object.YCenter
		VertexBuffer[arrayPos0].z = object.ZVectorPoint
		GetTableValue(object.XVectorPoint, arrayPos0, MainCover_VectorX)
		GetTableValue(object.YVectorPoint, arrayPos0, MainCover_VectorY)
		//GetTableValue(object.ZVectorPoint, arrayPos0, MainCover_VectorZ)
		VertexBuffer[arrayPos0].x += object.XVectorPoint
		VertexBuffer[arrayPos0].y += object.YVectorPoint
		//VertexBuffer[arrayPos0].z += object.ZVectorPoint
		
		arrayPos0++
	loop
end function

private function MainCover_DrawBackFace
		VertexBuffer[8].x = VertexBuffer[0].x
		VertexBuffer[8].y = VertexBuffer[0].y
		VertexBuffer[8].z = VertexBuffer[0].z

		VertexBuffer[9].x = VertexBuffer[4].x
		VertexBuffer[9].y = VertexBuffer[4].y
		VertexBuffer[9].z = VertexBuffer[4].z

		VertexBuffer[10].x = VertexBuffer[5].x
		VertexBuffer[10].y = VertexBuffer[5].y
		VertexBuffer[10].z = VertexBuffer[5].z

		VertexBuffer[11].x = VertexBuffer[1].x
		VertexBuffer[11].y = VertexBuffer[1].y
		VertexBuffer[11].z = VertexBuffer[1].z
end function

private function MainCover_DrawFrontFace
		VertexBuffer[12].x = VertexBuffer[3].x
		VertexBuffer[12].y = VertexBuffer[3].y
		VertexBuffer[12].z = VertexBuffer[3].z

		VertexBuffer[13].x = VertexBuffer[2].x
		VertexBuffer[13].y = VertexBuffer[2].y
		VertexBuffer[13].z = VertexBuffer[2].z

		VertexBuffer[14].x = VertexBuffer[6].x
		VertexBuffer[14].y = VertexBuffer[6].y
		VertexBuffer[14].z = VertexBuffer[6].z

		VertexBuffer[15].x = VertexBuffer[7].x
		VertexBuffer[15].y = VertexBuffer[7].y
		VertexBuffer[15].z = VertexBuffer[7].z
end function

private function MainCover_DrawLeftFace
		VertexBuffer[16].x = VertexBuffer[1].x
		VertexBuffer[16].y = VertexBuffer[1].y
		VertexBuffer[16].z = VertexBuffer[1].z

		VertexBuffer[17].x = VertexBuffer[5].x
		VertexBuffer[17].y = VertexBuffer[5].y
		VertexBuffer[17].z = VertexBuffer[5].z

		VertexBuffer[18].x = VertexBuffer[6].x
		VertexBuffer[18].y = VertexBuffer[6].y
		VertexBuffer[18].z = VertexBuffer[6].z

		VertexBuffer[19].x = VertexBuffer[2].x
		VertexBuffer[19].y = VertexBuffer[2].y
		VertexBuffer[19].z = VertexBuffer[2].z
end function

private function MainCover_DrawRightFace
		VertexBuffer[20].x = VertexBuffer[0].x
		VertexBuffer[20].y = VertexBuffer[0].y
		VertexBuffer[20].z = VertexBuffer[0].z

		VertexBuffer[21].x = VertexBuffer[3].x
		VertexBuffer[21].y = VertexBuffer[3].y
		VertexBuffer[21].z = VertexBuffer[3].z

		VertexBuffer[22].x = VertexBuffer[7].x
		VertexBuffer[22].y = VertexBuffer[7].y
		VertexBuffer[22].z = VertexBuffer[7].z

		VertexBuffer[23].x = VertexBuffer[4].x
		VertexBuffer[23].y = VertexBuffer[4].y
		VertexBuffer[23].z = VertexBuffer[4].z
end function

// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case SIGNPOST_IDLE
#platform: USE_ORIGINS
		object.fallbackTimer++
#endplatform
		
		temp0 = object.ypos
		temp0 += 0x400000
		if player[0].ypos < temp0
			stage.newXBoundary1 = object.xpos
			stage.newXBoundary1 >>= 16
			stage.newXBoundary1 -= screen.xcenter
			stage.newXBoundary1 -= screen.xcenter

			stage.newXBoundary2 = object.xpos
			stage.newXBoundary2 >>= 16
			stage.newXBoundary2 += screen.xcenter

			if player[0].xpos > object.xpos
				stage.newXBoundary1 = object.xpos
				stage.newXBoundary1 >>= 16
				stage.newXBoundary1 -= screen.xcenter
				
				// No achievements in debug mode!
				if stage.debugMode == false
					if stage.activeList == BONUS_STAGE
						// Boss Attack is on the Bonus Stage list, check for the Boss Attack achievement
						// (This code doesn't account for Origins having everything on the Bonus list since Missions didn't even exist back then)
						if stage.minutes < 7
							// Grant the "Scrambled Egg" Achievement
							CallNativeFunction2(SetAchievement, ACHIEVEMENT_SCRAMBLEDEGG, 100)
						end if
					else
						if stage.listPos == 0
							if stage.minutes == 0
								if stage.seconds < 35
									// Grant the "Quick Run" Achievement, for beating EHZ in under 35 seconds
									CallNativeFunction2(SetAchievement, ACHIEVEMENT_QUICKRUN, 100)
								end if
							end if
						end if
					end if
				end if

#platform: USE_ORIGINS
				CallNativeFunction2(NotifyCallback, NOTIFY_TOUCH_SIGNPOST, 0)
#endplatform
				stage.timeEnabled = false
				object.state = SIGNPOST_SPUN
#platform: USE_ORIGINS
				SignPost_initFallback = object.fallbackTimer
#endplatform
				PlaySfx(SfxName[Sign Post], false)
				
				if Player_superState == SUPERSTATE_SUPER
					Player_superState = SUPERSTATE_FADEOUT
					if stage.debugMode == false
						// Grant the "Superstar" Achievement, for beating an act while transformed
						CallNativeFunction2(SetAchievement, ACHIEVEMENT_SUPERSTAR, 100)
					end if
				end if
			end if
		end if
		break
		
	case SIGNPOST_SPUN
		music.volume--

		object.spinTimer++
		if object.spinTimer == 1
			object.spinTimer = 0
			object.frame++
			if object.frame == 10
				object.frame = 1
				
				Rand(temp0, 48)
				temp0 <<= 16
				temp0 += object.xpos
				temp0 -= 0x180000
				
				Rand(temp1, 32)
				temp1 <<= 16
				temp1 += object.ypos
				temp1 -= 0x140000
				
				CreateTempObject(TypeName[Ring Sparkle], 0, temp0, temp1)
				
				object.spinCount--
				if object.pointerZ == 127 // On front face?
					if object.spinCount <= 0
						SetBit(object.activePlayers, 0, true)
						if stage.player2Enabled == true
							SetBit(object.activePlayers, 1, true)
						end if
						object.state = SIGNPOST_EXITPLAYER
					end if
				end if
			end if
		end if
		break

	case SIGNPOST_EXITPLAYER
		temp0 = screen.xcenter
		temp0 <<= 16
		temp0 += object.xpos
		
#platform: USE_ORIGINS
		object.fallbackTimer++
		SignPost_fallbackCheck = object.fallbackTimer
		SignPost_fallbackCheck -= SignPost_initFallback
		if SignPost_fallbackCheck > 200
			PlayMusic(TRACK_ACTFINISH)
			object[SLOT_ACTFINISH].type = TypeName[Act Finish]
			object[SLOT_ACTFINISH].drawOrder = 6
			object[SLOT_ACTFINISH].priority = PRIORITY_ACTIVE
		end if
#endplatform
		
		if player[0].xpos > temp0
			PlayMusic(TRACK_ACTFINISH)
			object.state = SIGNPOST_FINISHED
			object[30].type = TypeName[Act Finish]
			object[30].drawOrder = 6
			object[30].priority = PRIORITY_ACTIVE
#platform: USE_ORIGINS
		else
			if player[0].animation == ANI_PUSHING
				PlayMusic(TRACK_ACTFINISH)
				object.state = SIGNPOST_FINISHED
				object[30].type = TypeName[Act Finish]
				object[30].drawOrder = 6
				object[30].priority = PRIORITY_ACTIVE
			end if
#endplatform
		end if
		break

	case SIGNPOST_IDLE_VS
		stage.newXBoundary2 = object.xpos
		stage.newXBoundary2 >>= 16
		stage.newXBoundary2 += screen.xcenter
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			GetBit(temp1, object.activePlayers, currentPlayer)
			if temp1 == false
				if player[currentPlayer].xpos > object.xpos
					object.priority = PRIORITY_ACTIVE
					
					SetBit(object.activePlayers, currentPlayer, true)
					
					if currentPlayer == 0
						if Player_superState == SUPERSTATE_SUPER
							Player_superState = SUPERSTATE_FADEOUT
						end if

						if vs.playerID == 0
							temp2 = 0
						else
							temp2 = 1
						end if
					else
						if vs.playerID == 0
							temp2 = 1
						else
							temp2 = 0
						end if
					end if

					if temp2 == 0
						vs.time1P = stage.milliSeconds
						vs.time1P = stage.seconds
						vs.time1P *= 100
						temp2 = stage.minutes
						temp2 *= 6000
						vs.time1P += temp2
						vs.time1P += stage.milliSeconds
					else
						vs.time2P = stage.milliSeconds
						vs.time2P = stage.seconds
						vs.time2P *= 100
						temp2 = stage.minutes
						temp2 *= 6000
						vs.time2P += temp2
						vs.time2P += stage.milliSeconds
					end if

					object.lastPlayerID = currentPlayer
					object.spinCount = 16
					object.state = SIGNPOST_SPUN_VS
					PlaySfx(SfxName[Sign Post], false)

					if object.allPlayersFinished == false
						PlaySfx(SfxName[Sign Post 2P], false)
						object[30].state = VSGAME_FINISH_COUNTDOWN
						vsGame[30].countdownPlayer = currentPlayer
						vsGame[30].countdownPlayer ^= 1
						switch player[currentPlayer].character
						case PLAYER_SONIC_A
							EditFrame(5, -24, -16, 48, 32, 34, 1)
							break
							
						case PLAYER_TAILS_A
							EditFrame(5, -24, -16, 48, 32, 34, 34)
							break
							
						case PLAYER_KNUCKLES_A
							EditFrame(5, -24, -16, 48, 32, 34, 67)
							break
							
#platform: USE_ORIGINS
						case PLAYER_AMY_A
							EditFrame(5, -24, -16, 48, 32, 34, 149)
							break
#endplatform
						end switch
					else
						object[30].state = VSGAME_DISPLAYTAGS
#platform: USE_ORIGINS
						stage.timeEnabled = false
#endplatform
					end if

					if object.activePlayers == 3 // both bits set? (3 is 0b11)
						object.allPlayersFinished	= true
						object[30].state 			= VSGAME_DISPLAYTAGS
					end if
				end if
			end if
		next
		break

	case SIGNPOST_SPUN_VS
		if object.allPlayersFinished == true
			music.volume--
		else
			foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
				GetBit(temp1, object.activePlayers, currentPlayer)
				if temp1 == false
					if player[currentPlayer].xpos > object.xpos
						object.spinTimer	= 1
						object.frame 	 	= 4
						object.spinCount 	= 1
					end if
				end if
			next
		end if

		object.spinTimer++
		if object.spinTimer == 1
			object.spinTimer = 0
			object.frame++
			if object.frame == 10
				object.frame = 1
				Rand(temp0, 48)
				temp0 <<= 16
				temp0 += object.xpos
				temp0 -= 0x180000
				Rand(temp1, 32)
				temp1 <<= 16
				temp1 += object.ypos
				temp1 -= 0x140000
				CreateTempObject(TypeName[Ring Sparkle], 0, temp0, temp1)
				
				object.spinCount--
				if object.pointerZ == 127 // On front face?
					if object.spinCount == 0
						if object.allPlayersFinished == false
							object.allPlayersFinished = true
							object.state = SIGNPOST_IDLE_VS
						
#platform: USE_STANDALONE
							if object.lastPlayerID == 0
								// Start spectating P2
								camera[0].target 			= 1
								player[1].scrollTracking 	= true
								object[9].type = TypeName[Blank Object]
								CreateTempObject(TypeName[VS Game], 0, object.xpos, object.ypos)
								vsGame[tempObjectPos].timer = 384
								object[tempObjectPos].state = VSGAME_FADEOUT
								camera[0].xpos = player[1].xpos
								camera[0].xpos >>= 16
								camera[0].ypos = player[1].ypos
								camera[0].ypos >>= 16
							end if
#endplatform
						else
							object.state = SIGNPOST_FINISHED
							object[30].state = VSGAME_GOTOVSRESULTS
							vsGame[30].timer = -768
						end if
					end if
				end if
			end if
		end if
		break

	case SIGNPOST_FINISHED
		break

	case SIGNPOST_WINK
		object.timer++
		if object.timer < 15
			object.animationTimer++
			if object.animationTimer > 2
				object.frame++
				object.animationTimer = 0
			end if
			if object.frame < 3
				object.frame = 3
			end if
			if object.frame > 11
				object.frame = 11
			end if
		end if
		if object.timer > 16
			object.frame = 18
		end if
		break
	end switch
	
	// Invisible off-screen barrier
	
	// Bug Details:
	// 212 in the line "temp0 += 212" is supposed to be the center of the screen when the screen width is set to 424 (the default screen width in RSDK)
	// For some reason, it's set as a constant rather than using "screen.xcenter", meaning the barrier will always be 252 pixels away from the sign post regardless of screen width
	// If your screen width is set to over 520, the barrier won't reach the edge of the screen, softlocking the game during exit stage right...
	// We decided to provide a decomp-exclusive fix, so you shouldn't have to worry about this there
	
	temp0 = 40
	temp0 += 212
#platform: USE_DECOMP
	temp0 = 40
	temp0 += screen.xcenter
#endplatform
	temp0 <<= 16
	temp0 += object.xpos
	currentPlayer = 0
	while currentPlayer < playerCount
		GetBit(temp1, object.activePlayers, currentPlayer)
		if temp1 == true
			player[currentPlayer].interaction 	= false
			player[currentPlayer].controlMode 	= CONTROLMODE_NONE
			player[currentPlayer].up 			= false
			player[currentPlayer].down 			= false
			player[currentPlayer].left 			= false
			player[currentPlayer].right 		= true
			player[currentPlayer].jumpPress 	= false
			player[currentPlayer].jumpHold 		= false
		end if
	
#platform: USE_DECOMP
		// Another ultrawide screen fix, this time for when the screen is wide enough to extend beyond
		// the stage width, causing the player to constantly fall and wrap around the stage vertically
		if player[currentPlayer].xpos > object.xpos
			if player[currentPlayer].gravity == GRAVITY_AIR
				temp1 = object.ypos
				temp1 += 0x200000 // This is quite a bit under the floor - this is because some acts have wavy ground at the end
				if player[currentPlayer].animation == ANI_JUMPING
					temp2 = player[currentPlayer].jumpOffset
					temp2 <<= 16
					temp1 -= temp2
				end if
				if player[currentPlayer].ypos > temp1
					player[currentPlayer].ypos	= temp1
					player[currentPlayer].yvel	= 0
				end if
			end if
		end if
#endplatform

		if player[currentPlayer].xpos > temp0
			player[currentPlayer].xpos 	= temp0
			player[currentPlayer].speed	= 0
			player[currentPlayer].xvel 	= 0
		end if

		if player[currentPlayer].type == TypeName[Debug Mode]
			SetBit(object.activePlayers, currentPlayer, false)
		end if
		
		currentPlayer++
	loop
	
#platform: USE_ORIGINS
	if game.playMode == BOOT_PLAYMODE_MISSION
		if object.state > SIGNPOST_IDLE
			player[0].invincibleTimer = 80
		end if
	end if
#endplatform
end event


event ObjectDraw

	//object.state = SIGNPOST_SPUN
	//object.spinCount = 18

	switch object.state
	
	case SIGNPOST_IDLE
		DrawSprite(0)
		DrawSprite(1)
		DrawSprite(2)
		break
	case SIGNPOST_SPUN
		CallFunction(MainCover_GetBoxCenter)

		temp0 = object.spinCount
		temp0 *= 2
		temp0 /= 8
		temp0 *= 8
		if temp0 > 8
			object.pointerZ += temp0
		else
			object.pointerZ += 8
		end if
		//object.pointerZ = 0
		//object.pointerZ = 127
		//object.pointerZ = 255
		//object.pointerZ = 383
		
		//object.pointerX /= 8
		//object.pointerY /= 8
		object.pointerZ &= 0x1FF
		CheckEqual(object.invertRot, true)
		temp0 = checkResult
		CheckLower(object.pointerZ, 129)
		temp0 &= checkResult
		CheckGreater(object.pointerZ, 120)
		temp0 &= checkResult
		if temp0 == true
			object.pointerZ = 128
			object.ZLock = true
		end if

		// Rotate the points
		CallFunction(MainCover_GetRotation)

		CallFunction(MainCover_DrawTopBotFaces)
		CallFunction(MainCover_DrawBackFace)
		CallFunction(MainCover_DrawFrontFace)
		CallFunction(MainCover_DrawLeftFace)
		CallFunction(MainCover_DrawRightFace)

		// I couldn't tell you why this exact arithmetic gets the correct vertical scrolling speed for the signs, but it does.
		temp0 = camera[0].xpos
		temp0 -= object.ixpos
		temp0 *= 640
		temp0 /= 265
		temp0 -= 3
		
		temp2 = object.iypos
		temp2 -= camera[0].ypos
		temp2 += screen.ycenter
		temp2 -= 135
		temp2 *= 640
		temp2 /= 120
		
		MatrixTranslateXYZ(MAT_WORLD, temp0, -0xfffff, temp2)
		MatrixRotateXYZ(MAT_VIEW, -128, 0, 256)
		scene3D.vertexCount = 24
		scene3D.faceCount   = 6

		DrawSprite(0)
		DrawSprite(1)
		Draw3DScene()
		break
	
	case SIGNPOST_WINK
		DrawSprite(0)
		DrawSprite(1)
		DrawSprite(object.frame)
		break
		
	default
		DrawSprite(0)
		DrawSprite(1)
		DrawSprite(3)
		break
		
	end switch
end event


event ObjectStartup
	LoadSpriteSheet("Global/Items.gif")

	scene3D.vertexCount = 0
	scene3D.faceCount   = 0
	
	// reset matrix
	MatrixTranslateXYZ(MAT_WORLD, 0, 0, 0)
	MatrixRotateZ(MAT_WORLD, 0)
	ArrayPos0 = 0
	ArrayPos1 = 0

	// Face corner setup
	while ArrayPos0 < 24
		// Top Left
		FaceBuffer[ArrayPos1].a  = ArrayPos0
		// Top Right
		FaceBuffer[ArrayPos1].b  = ArrayPos0
		FaceBuffer[ArrayPos1].b += 2
		// Bottom Left
		FaceBuffer[ArrayPos1].c  = ArrayPos0
		FaceBuffer[ArrayPos1].c += 1
		// Bottom Right
		FaceBuffer[ArrayPos1].d  = ArrayPos0
		FaceBuffer[ArrayPos1].d += 3
	
		ArrayPos1++
		ArrayPos0 += 4
	loop

	// Face rendering
	FaceBuffer[0].Flag  = FACE_COLOURED_3D
	faceBuffer[0].Color = 0x00303030

	FaceBuffer[1].Flag  = FACE_COLOURED_3D
	faceBuffer[1].Color = 0x00303030

	FaceBuffer[2].Flag  = FACE_TEXTURED_3D
	faceBuffer[2].Color = 0xFF303030

	FaceBuffer[3].Flag  = FACE_TEXTURED_3D
	faceBuffer[3].Color = 0xFF303030

	FaceBuffer[4].Flag  = FACE_TEXTURED_3D
	faceBuffer[4].Color = 0xFF303030

	FaceBuffer[5].Flag  = FACE_TEXTURED_3D
	faceBuffer[5].Color = 0xFF303030

	// Back Face
		// Starting co-ords
		temp0 = 356
		temp1 = 353
		
		// Top-left
		VertexBuffer[20].u = temp0
		VertexBuffer[20].v = temp1
		
		// Bottom-left
		temp1 += 30
		VertexBuffer[23].u = temp0
		VertexBuffer[23].v = temp1
		
		// Bottom-right
		temp0 += 48
		VertexBuffer[22].u = temp0
		VertexBuffer[22].v = temp1
		
		// Top-right
		temp1 -= 30
		VertexBuffer[21].u = temp0
		VertexBuffer[21].v = temp1

	// Side 1
		// Starting co-ords
		temp0 = 503
		temp1 = 384
		
		// Top-left
		VertexBuffer[13].u = temp0
		VertexBuffer[13].v = temp1
		
		// Bottom-left
		temp1 += 30
		VertexBuffer[14].u = temp0
		VertexBuffer[14].v = temp1
		
		// Bottom-right
		temp0 += 8
		VertexBuffer[15].u = temp0
		VertexBuffer[15].v = temp1
		
		// Top-right
		temp1 -= 30
		VertexBuffer[12].u = temp0
		VertexBuffer[12].v = temp1
		
	// Side 2
		// Starting co-ords
		temp0 = 503
		temp1 = 384
		
		// Top-left
		VertexBuffer[8].u = temp0
		VertexBuffer[8].v = temp1
		
		// Bottom-left
		temp1 += 30
		VertexBuffer[9].u = temp0
		VertexBuffer[9].v = temp1
		
		// Bottom-right
		temp0 += 8
		VertexBuffer[10].u = temp0
		VertexBuffer[10].v = temp1
		
		// Top-right
		temp1 -= 30
		VertexBuffer[11].u = temp0
		VertexBuffer[11].v = temp1

	// account for all vertices and faces
	scene3D.vertexCount += 24
	scene3D.faceCount   += 6

	scene3D.projectionX  = 435000
	scene3D.projectionY  = 198000
	
	SpriteFrame(-24, 16, 48, 16, 356, 383)
	SpriteFrame(-24, -16, 48, 2, 356, 351)
	SpriteFrame(-24, -14, 48, 30, 356, 353)
	switch stage.playerListPos
	case PLAYER_SONIC_A
		SpriteFrame(-24, -14, 48, 30, 405, 353)

		// Wink Frames
		SpriteFrame(-24, -14, 48, 30, 413, 19)
		SpriteFrame(-24, -14, 48, 30, 462, 19)
		SpriteFrame(-24, -14, 48, 30, 413, 50)
		SpriteFrame(-24, -14, 48, 30, 462, 50)
		SpriteFrame(-24, -14, 48, 30, 413, 50)
		SpriteFrame(-24, -14, 48, 30, 462, 19)
		SpriteFrame(-24, -14, 48, 30, 413, 19)
		
		SpriteFrame(-24, -14, 48, 30, 405, 353)
		temp0 = 405
		temp1 = 353
		break
		
	case PLAYER_TAILS_A
		SpriteFrame(-24, -14, 48, 30, 454, 353)

		// Wink Frames
		SpriteFrame(-24, -14, 48, 30, 413, 81)
		SpriteFrame(-24, -14, 48, 30, 462, 81)
		SpriteFrame(-24, -14, 48, 30, 413, 112)
		SpriteFrame(-24, -14, 48, 30, 462, 112)
		SpriteFrame(-24, -14, 48, 30, 413, 112)
		SpriteFrame(-24, -14, 48, 30, 462, 81)
		SpriteFrame(-24, -14, 48, 30, 413, 81)
		
		SpriteFrame(-24, -14, 48, 30, 454, 353)
		temp0 = 454
		temp1 = 353
		break
		
	case PLAYER_KNUCKLES_A
		SpriteFrame(-24, -14, 48, 30, 405, 384)

		// Wink Frames
		SpriteFrame(-24, -14, 48, 30, 413, 143)
		SpriteFrame(-24, -14, 48, 30, 462, 143)
		SpriteFrame(-24, -14, 48, 30, 413, 174)
		SpriteFrame(-24, -14, 48, 30, 462, 174)
		SpriteFrame(-24, -14, 48, 30, 413, 174)
		SpriteFrame(-24, -14, 48, 30, 462, 143)
		SpriteFrame(-24, -14, 48, 30, 413, 143)
		
		SpriteFrame(-24, -14, 48, 30, 405, 384)
		temp0 = 405
		temp1 = 384
		break
		
#platform: USE_ORIGINS
	case PLAYER_AMY_A
		SpriteFrame(-24, -14, 48, 30, 454, 384)

		// Wink Frames
		SpriteFrame(-24, -14, 48, 30, 413, 205)
		SpriteFrame(-24, -14, 48, 30, 462, 205)
		SpriteFrame(-24, -14, 48, 30, 413, 236)
		SpriteFrame(-24, -14, 48, 30, 462, 236)
		SpriteFrame(-24, -14, 48, 30, 413, 236)
		SpriteFrame(-24, -14, 48, 30, 462, 205)
		SpriteFrame(-24, -14, 48, 30, 413, 205)
		
		SpriteFrame(-24, -14, 48, 30, 454, 384)
		temp0 = 454
		temp1 = 384
		break
#endplatform
	end switch
	
	// Front face of 3D spinning model. temp0 and temp1 are set when creating the spriteframes
		// Top-left
		VertexBuffer[16].u = temp0
		VertexBuffer[16].v = temp1
		
		// Bottom-left
		temp1 += 30
		VertexBuffer[17].u = temp0
		VertexBuffer[17].v = temp1
		
		// Bottom-right
		temp0 += 48
		VertexBuffer[18].u = temp0
		VertexBuffer[18].v = temp1
		
		// Top-right
		temp1 -= 30
		VertexBuffer[19].u = temp0
		VertexBuffer[19].v = temp1

	foreach (TypeName[Sign Post], arrayPos0, ALL_ENTITIES)
		CheckGreater(object[arrayPos0].propertyValue, 0)
		temp0 = checkResult
		CheckEqual(options.vsMode, false)
		temp0 &= checkResult
		if temp0 == true
			object[arrayPos0].type = TypeName[Blank Object]
		else
			object[arrayPos0].frame = 1
			object[arrayPos0].spinCount = 16
			object[arrayPos0].priority = PRIORITY_XBOUNDS
			object[arrayPos0].pointerZ += 383
			if object[arrayPos0].propertyValue != 2
				object[arrayPos0].priority = PRIORITY_XBOUNDS
			end if
		end if

		if options.vsMode == true
			object[arrayPos0].state = SIGNPOST_IDLE_VS
		end if
	next

	SetTableValue(TypeName[Sign Post], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(SignPost_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(SignPost_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKEdit
	if editor.returnVariable == true
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			checkResult = object.propertyValue
			break
			
		case 0 // type
			checkResult = object.propertyValue
			break
			
		end switch
	else
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			object.propertyValue = editor.variableValue
			break
			
		case 0 // type
			object.propertyValue = editor.variableValue
			break
			
		end switch
	end if
end event


event RSDKDraw
	DrawSprite(0)
	DrawSprite(1)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Items2.gif")
	SpriteFrame(-24, 16, 48, 16, 34, 132)
	SpriteFrame(-24, -16, 48, 32, 34, 100)

	AddEditorVariable("type")
	SetActiveVariable("type")
	AddEnumVariable("Normal", 0)
	AddEnumVariable("VS Only (No Y Bounds)", 1)
	AddEnumVariable("VS Only (Use Y Bounds)", 2)
end event
