//-----------------Sonic CD Water Burst Script----------------//
//--------Scripted by Christian Whitehead 'The Taxman'--------//
//-------Unpacked By Rubberduckycooly's Script Unpacker-------//

// Aliases
private alias object.value0 : object.frameTimer
private alias object.value1 : object.timer
private alias object.value1 : BurstSplash.xvel
private alias object.value2 : BurstSplash.yvel
private alias object.value3 : player.timer

// Player Aliases
private alias object.state			: player.state
private alias object.animation			: player.animation
private alias object.prevAnimation		: player.prevAnimation
private alias object.controlMode		: player.controlMode
private alias object.left			: player.left
private alias object.right			: player.right
private alias object.up				: player.up
private alias object.down			: player.down
private alias object.timer			: player.timer
private alias object.xpos			: player.xpos
private alias object.ypos			: player.ypos
private alias object.xvel 			: player.xvel
private alias object.yvel 			: player.yvel
private alias object.speed			: player.speed

// Control Mode
private alias -1 : CONTROLMODE_NONE
private alias 0 : CONTROLMODE_P1

// Priority
private alias 3	: PRIORITY_XBOUNDS

event ObjectUpdate
	object.timer++
	if object.timer == 8
		Rand(temp0, 80)
		temp0  -= 40
		temp0 <<= 16
		temp0  += object.xpos

		Rand(temp1, 16)
		temp1  -= 8
		temp1 <<= 16
		temp1  += object.ypos
		temp1  -= 0x300000

		CreateTempObject(TypeName[Burst Splash], 0, temp0, temp1)
		Rand(BurstSplash[tempObjectPos].xvel, 10)
		BurstSplash[tempObjectPos].xvel -= 5
		BurstSplash[tempObjectPos].xvel <<= 13
		BurstSplash[tempObjectPos].yvel = -0x20000
		object.timer = 0
	end if
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_PLATFORM, object.entityPos, -48, -66, 48, -32, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
		if CheckResult == true
			player[currentPlayer].ypos += 0x20000
		end if
	next
end event

event ObjectDraw
	object.ypos = Stage.NewWaterLevel
	object.ypos -= 0x300000 // Y - 48 in map coords

	temp0 = oscillation
	temp0 <<= 2
	Sin(temp1, temp0)
	temp1 <<= 9
	object.ypos += temp1

	temp0  = object.frameTimer
	temp0 /= 3
	temp0 += 4
	DrawSprite(temp0)

	temp0 += 4
	DrawSprite(temp0)

	temp0 += 4
	DrawSprite(temp0)

	temp0 += 4
	DrawSprite(temp0)

	object.frameTimer++
	object.frameTimer %= 12
	DrawSprite(object.frame)

	temp0  = object.frame
	temp0 += 4
	DrawSprite(temp0)
	
	object.frame++
	object.frame &= 3
end event

event ObjectStartup
	LoadSpriteSheet("LZ/Objects3.gif")
	SpriteFrame(-56, -80, 112, 32, 90, 59)		// #0  - Water Burst frame 0
	SpriteFrame(-56, -80, 112, 32, 90, 92)		// #1  - Water Burst frame 1
	SpriteFrame(-56, -80, 112, 32, 90, 125)	// #2  - Water Burst frame 2
	SpriteFrame(-56, -80, 112, 32, 90, 158)	// #3  - Water Burst frame 3

	SpriteFrame(-56, 48, 112, 32, 90, 59)		// #4  - Current frame 0
	SpriteFrame(-56, 48, 112, 32, 90, 92)		// #5  - Current frame 1
	SpriteFrame(-56, 48, 112, 32, 90, 125)		// #6  - Current frame 2
	SpriteFrame(-56, 48, 112, 32, 90, 158)		// #7  - Current frame 3

	SpriteFrame(-32, -64, 64, 32, 3, 191)		// #8  - Current frame 4
	SpriteFrame(-32, -64, 64, 32, 68, 191)		// #9  - Current frame 5
	SpriteFrame(-32, -64, 64, 32, 133, 191)		// #10 - Current frame 6
	SpriteFrame(-32, -64, 64, 32, 133, 224)		// #11 - Current frame 7

	SpriteFrame(-32, -32, 64, 32, 3, 191)		// #12 - Current frame 8
	SpriteFrame(-32, -32, 64, 32, 68, 191)		// #13 - Current frame 9
	SpriteFrame(-32, -32, 64, 32, 133, 191)		// #14 - Current frame 10
	SpriteFrame(-32, -32, 64, 32, 133, 224)		// #15 - Current frame 11

	SpriteFrame(-32, 0, 64, 32, 3, 191)		// #16 - Current frame 12
	SpriteFrame(-32, 0, 64, 32, 68, 191)		// #17 - Current frame 13
	SpriteFrame(-32, 0, 64, 32, 133, 191)		// #18 - Current frame 14
	SpriteFrame(-32, 0, 64, 32, 133, 224)		// #19 - Current frame 15

	SpriteFrame(-32, 32, 64, 32, 3, 191)		// #20 - Current frame 16
	SpriteFrame(-32, 32, 64, 32, 68, 191)		// #21 - Current frame 17
	SpriteFrame(-32, 32, 64, 32, 133, 191)		// #22 - Current frame 18
	SpriteFrame(-32, 32, 64, 32, 133, 224)		// #23 - Current frame 19

	ArrayPos0 = 32
	while ArrayPos0 < 1056
		if Object[ArrayPos0].Type == TypeName[Water Burst]
			Object[ArrayPos0].DrawOrder = 4
			Object[ArrayPos0].Priority = PRIORITY_XBOUNDS
		end if
		ArrayPos0++
	loop
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Display.gif")
	SpriteFrame(-16, -16, 32, 32, 1, 143)		// #0 - "Script" Icon

	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
