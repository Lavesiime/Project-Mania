// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Harpoon H Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer
private alias object.value1 : object.hitboxL
private alias object.value2 : object.hitboxR
private alias object.value10 : object.smoothMovement

// For LZSetup
private alias object.value12 : object.harpoonTimer
private alias object.value13 : object.harpoonAniTimer
private alias object.value14 : object.harpoonAniTimer2

// ========================
// Function Declarations
// ========================

reserve function HarpoonH_DebugDraw
reserve function HarpoonH_DebugSpawn


// ========================
// Tables
// ========================

private table HarpoonH_delayTable
	64, 6, 64, 6
end table

private table HarpoonH_frameTable
	0, 1, 2, 1
end table

private table HarpoonH_sizeTable
	8, 24, 40, 24
end table


// ========================
// Function Definitions
// ========================

private function HarpoonH_DebugDraw
	DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
end function


private function HarpoonH_DebugSpawn
	CreateTempObject(TypeName[Harpoon H], 0, object.xpos, object.ypos)
	object[tempObjectPos].direction = object.direction
end function


// ========================
// Events
// ========================

event ObjectUpdate
	object.timer = object[10].harpoonTimer
	object.animationTimer = object[10].harpoonAniTimer
	object.value6 = object[10].harpoonAniTimer2

	if object.animationTimer == 0
		GetTableValue(object.frame, object.timer, HarpoonH_frameTable)
	else
		if object.animationTimer == 1
			if object.value6 == 0
				if object.timer == 0
					PlaySfx(SfxName[Spear], false)
				end if
				if object.timer == 3
					PlaySfx(SfxName[Spear], false)
				end if
			end if
		end if
	end if

	object.hitboxL = -8
	GetTableValue(object.hitboxR, object.timer, HarpoonH_sizeTable)
	GetBit(temp0, object.direction, 0)

	if temp0 == true
		FlipSign(object.hitboxL)
		FlipSign(object.hitboxR)
		temp0 = object.hitboxR
		object.hitboxR = object.hitboxL
		object.hitboxL = temp0
	end if

	// Check interaction with players
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, object.hitboxL, -4, object.hitboxR, 4, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
		if checkResult == true
			CallFunction(Player_Hit)
		end if
	next
end event


event ObjectDraw
	object.smoothMovement = true
	
	if object.smoothMovement == true
		temp0 = object.xpos
		if object.timer == 0
			GetBit(temp2, object.direction, 0)
			if temp2 == true // Is FLIP_Y set?
				temp0 += 0x180000
			else
				temp0 -= 0x180000
			end if
		end if
		if object.timer == 1
			temp1 = 0x040000
			temp3 = object.animationTimer
			temp3 += 1
			temp1 *= temp3
			GetBit(temp2, object.direction, 0)
			if temp2 == false // Is FLIP_Y set?
				temp0 -= temp1
				temp0 += 0x040000
			else
				temp0 += temp1
				temp0 -= 0x040000
			end if
		end if
		if object.timer == 3
			temp1 = 0x040000
			temp3 = object.animationTimer
			temp3 += 1
			temp1 *= temp3
			GetBit(temp2, object.direction, 0)
			if temp2 == false // Is FLIP_Y set?
				temp0 -= 0x1C0000
				temp0 += temp1
			else
				temp0 += 0x1C0000
				temp0 -= temp1
			end if
		end if
		
		DrawSpriteFX(3, FX_FLIP, temp0, object.ypos)
		DrawSpriteFX(4, FX_FLIP, object.xpos, object.ypos)
	else
		DrawSpriteFX(object.frame, FX_FLIP, object.xpos, object.ypos)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("LZ/Objects2.gif")
	
	// Harpoon frames
	SpriteFrame(-8, -4, 16, 8, 196, 129)
	SpriteFrame(-8, -4, 32, 8, 158, 163)
	SpriteFrame(-8, -4, 48, 8, 94, 115)

	SpriteFrame(-5, -4, 42, 8, 214, 115) // Smooth movement frames
	SpriteFrame(-8, -4, 6, 8, 207, 115)

	// Add this object to the debug item list
	SetTableValue(TypeName[Harpoon H], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(HarpoonH_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(HarpoonH_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
end event


event RSDKLoad
	LoadSpriteSheet("LZ/Objects.gif")
	SpriteFrame(-8, -4, 48, 8, 94, 115)
	
	// Direction attr should be used
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
