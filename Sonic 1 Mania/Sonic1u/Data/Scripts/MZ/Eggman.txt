// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Eggman Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// Using MZEggman as a prefix here because there's like 12 "Eggman" objects in the game so its less confusing this way

// ========================
// Aliases
// ========================

private alias object.value0  : object.timer
private alias object.value1  : object.health
private alias object.value2  : object.invincibilityTimer
private alias object.value3  : object.flameAnim
private alias object.value4  : object.flameAnimTimer
private alias object.value5  : object.boundsL
private alias object.value6  : object.boundsR
private alias object.value7  : object.divePos.y
private alias object.value8  : object.originPos.y
private alias object.value9  : object.fireballDropDelay
private alias object.value10 : object.dontRandomizeNextDrop
private alias object.value11 : object.nextState
private alias object.value12 : object.hitDelay
private alias object.value13 : object.eggmanFrame
private alias object.value14 : object.mobileAnimTimer
private alias object.value15 : object.flameFrame
private alias object.value16 : object.equipmentFrame
private alias object.value17 : object.equipmentAnimTimer
private alias object.value18 : object.mobileFrame

// Eggman States
private alias 0  : MZEGGMAN_AWAITPLAYER
private alias 1  : MZEGGMAN_ENTEREGGMAN
private alias 2  : MZEGGMAN_TAUNT
private alias 3  : MZEGGMAN_MOVE_L
private alias 4  : MZEGGMAN_DROPFIREBALL_L
private alias 5  : MZEGGMAN_DROPPEDFIREBALL_L
private alias 6  : MZEGGMAN_MOVE_R
private alias 7  : MZEGGMAN_DROPFIREBALL_R
private alias 8  : MZEGGMAN_DROPPEDFIREBALL_R
private alias 9  : MZEGGMAN_EXPLODE
private alias 10 : MZEGGMAN_DEFEATFALL
private alias 11 : MZEGGMAN_DEFEATRISE
private alias 12 : MZEGGMAN_FLEE
private alias 13 : MZEGGMAN_ESCAPE
private alias 14 : MZEGGMAN_HITFREEZE

// Eggman Animations
private alias 0 : MZEGGANI_INVISIBLE
private alias 1 : MZEGGANI_IDLE
private alias 2 : MZEGGANI_LAUGH
private alias 3 : MZEGGANI_LAUGH_LOOP
private alias 4 : MZEGGANI_HIT
private alias 5 : MZEGGANI_DEFEATED
private alias 6 : MZEGGANI_TOASTED
private alias 7 : MZEGGANI_PANIC

// Flame Animations
private alias 0 : FLAME_INACTIVE
private alias 1 : FLAME_ACTIVE
private alias 2 : FLAME_EXPLODE

// Marker aliases
private alias object.xpos  : marker.xpos
private alias object.ypos  : marker.ypos
private alias object.ixpos : marker.ixpos
private alias object.iypos : marker.iypos

// Fireball aliases
private alias object.value2 : fireball.fireballDir
private alias object.value3 : fireball.finalY
private alias 1 : FIREBALL_MOVE_GRAVITY

// Player Aliases
private alias object.xpos : player.xpos
private alias object.xvel : player.xvel
private alias object.speed : player.speed
private alias object.animation : player.animation
private alias object.collisionRight : player.collisionRight

private alias object.value18 : player.sortedDrawOrder
private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom

// Music Events
private alias 26 : SLOT_MUSICEVENT_BOSS

private alias 0 : MUSICEVENT_FADETOBOSS
private alias 1 : MUSICEVENT_FADETOSTAGE
private alias 2 : MUSICEVENT_TRANSITION


// ========================
// Function Declarations
// ========================

reserve function MZEggman_Oscillate
reserve function MZEggman_HandleFireballErupt


// ========================
// Aliases
// ========================

private function MZEggman_Oscillate
	Sin256(object.ypos, object.angle)
	object.ypos <<= 10
	object.ypos += object.originPos.y
	object.angle += 2
	object.angle &= 0xFF
end function


public function MZEggman_HandleFireballErupt
	if object.fireballDropDelay > 0
		object.fireballDropDelay--
	else
		Rand(temp0, 112)
		temp0 -= 56
		temp0 <<= 16
		temp0 += marker[+1].xpos
		temp1 = marker[+1].ypos
		temp1 += 0x200000
		CreateTempObject(TypeName[Fireball], 0, temp0, temp1)
		object[tempObjectPos].state = FIREBALL_MOVE_GRAVITY
		object[tempObjectPos].xvel = 0
		object[tempObjectPos].yvel = -0x40000
		fireball[tempObjectPos].finalY = temp1
		fireball[tempObjectPos].fireballDir = 2
		PlaySfx(SfxName[Small Fireball], false)

		if object.dontRandomizeNextDrop == false
			Rand(temp0, 3)
			if temp0 == 2
				object.fireballDropDelay = 70
				object.dontRandomizeNextDrop = true
			else
				object.fireballDropDelay = 140
			end if
		else
			object.dontRandomizeNextDrop = false
			object.fireballDropDelay = 140
		end if
	end if
end function


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case MZEGGMAN_AWAITPLAYER
		temp0 = marker[+1].ixpos
		temp0 += screen.xcenter
		stage.newXBoundary2 = temp0
		
		temp0 -= screen.xsize
		stage.newXBoundary1 = temp0
		
		temp0 = stage.curYBoundary2
		temp0 -= screen.ysize
		stage.newYBoundary1 = temp0
		
		if object[0].xpos > marker[+1].xpos
			object.divePos.y = marker[+1].ypos
			object.divePos.y -= 0x900000
			object.originPos.y = marker[+1].ypos
			object.originPos.y -= 0x900000

			temp0 = 0x700000
			temp0 *= screen.xcenter
			temp0 /= 160
			object.speed = 0x20000
			object.speed *= screen.xcenter
			object.speed /= 160

			object.boundsL = marker[+1].xpos
			object.boundsL -= temp0
			object.boundsR = marker[+1].xpos
			object.boundsR += temp0

			object.xvel = -0x10000
			object.flameAnim = FLAME_ACTIVE
			object.fireballDropDelay = 120
			ResetObjectEntity(SLOT_MUSICEVENT_BOSS, TypeName[Music Event], MUSICEVENT_FADETOBOSS, 0, 0)
			// 26 = SLOT_MUSICEVENT_BOSS
			object[26].priority = PRIORITY_ACTIVE
			object.drawOrder = 4
			player[0].sortedDrawOrder = 3
			if object[1].type == TypeName[Player 2 Object]
				player[1].sortedDrawOrder = 3
			end if
			object.state++
		end if
		break

	case MZEGGMAN_ENTEREGGMAN
		object.xpos += object.xvel
		if object.xpos <= object.boundsR
			object.xpos = object.boundsR
			object.flameAnim = FLAME_INACTIVE
			object.state++
		end if
		CallFunction(MZEggman_Oscillate)
		break

	case MZEGGMAN_TAUNT
		CallFunction(MZEggman_Oscillate)

		object.timer++
		if object.timer == 16
			object.timer = 0
			object.xvel = object.speed
			FlipSign(object.xvel)
			object.yvel = 0x10000
			object.flameAnim = FLAME_ACTIVE
			object.state++
		end if
		break

	case MZEGGMAN_MOVE_L
		object.yvel -= 0x400
		object.xpos += object.xvel
		object.ypos += object.yvel

		if object.xpos <= object.boundsL
			object.xpos = object.boundsL
			object.flameAnim = FLAME_INACTIVE
			object.state++
		end if
		CallFunction(MZEggman_HandleFireballErupt)
		break

	case MZEGGMAN_DROPFIREBALL_L
		object.yvel -= 0x400
		object.ypos += object.yvel

		if object.ypos <= object.divePos.y
			object.ypos = object.divePos.y
			object.direction = FACING_LEFT
			object.animation = MZEGGANI_LAUGH_LOOP
			object.animationTimer = 0
			CreateTempObject(TypeName[Boss Fireball], 0, object.xpos, object.ypos)
			object[tempObjectPos].ypos += 0x140000
			object.state++
		end if
		CallFunction(MZEggman_HandleFireballErupt)
		break

	case MZEGGMAN_DROPPEDFIREBALL_L
		object.timer++
		if object.timer == 80
			object.timer = 0
			object.xvel = object.speed
			object.yvel = 0x10000
			object.animation = MZEGGANI_IDLE
			object.flameAnim = FLAME_ACTIVE
			object.state++
		end if

		CallFunction(MZEggman_HandleFireballErupt)
		break

	case MZEGGMAN_MOVE_R
		object.yvel -= 0x400
		object.xpos += object.xvel
		object.ypos += object.yvel

		if object.xpos >= object.boundsR
			object.xpos = object.boundsR
			object.flameAnim = FLAME_INACTIVE
			object.state++
		end if
		CallFunction(MZEggman_HandleFireballErupt)
		break

	case MZEGGMAN_DROPFIREBALL_R
		object.yvel -= 0x400
		object.ypos += object.yvel

		if object.ypos <= object.divePos.y
			object.ypos = object.divePos.y
			object.direction = FACING_RIGHT
			object.animation = MZEGGANI_LAUGH_LOOP
			object.animationTimer = 0
			CreateTempObject(TypeName[Boss Fireball], 0, object.xpos, object.ypos)
			object[tempObjectPos].ypos += 0x140000
			object.state++
		end if
		CallFunction(MZEggman_HandleFireballErupt)
		break

	case MZEGGMAN_DROPPEDFIREBALL_R
		object.timer++
		if object.timer == 80
			object.timer = 0
			object.xvel = object.speed
			FlipSign(object.xvel)
			object.yvel = 0x10000
			object.animation = MZEGGANI_IDLE
			object.flameAnim = FLAME_ACTIVE
			object.state = MZEGGMAN_MOVE_L
		end if

		CallFunction(MZEggman_HandleFireballErupt)
		break

	case MZEGGMAN_EXPLODE
		object.timer++
		temp0 = object.timer
		temp0 &= 7
		if temp0 == 4
			Rand(temp0, 64)
			temp0 -= 32
			temp0 <<= 16
			temp0 += object.xpos
			Rand(temp1, 48)
			temp1 -= 24
			temp1 <<= 16
			temp1 += object.ypos
			CreateTempObject(TypeName[Explosion], 0, temp0, temp1)
			PlaySfx(SfxName[Explosion], false)
		end if

		if object.timer == 180
			object.animation = MZEGGANI_TOASTED
			object.animationTimer = 0
			object.timer = 0
			object.direction = FACING_LEFT
			ResetObjectEntity(SLOT_MUSICEVENT_BOSS, TypeName[Music Event], MUSICEVENT_FADETOSTAGE, 0, 0)
			// 26 = SLOT_MUSICEVENT_BOSS
			object[26].priority = PRIORITY_ACTIVE
			object.state++
		end if
		break

	case MZEGGMAN_DEFEATFALL
		object.ypos += object.yvel
		object.yvel += 0x1800

		object.timer++
		if object.timer == 32
			object.yvel = 0
			object.timer = 0
			object.state++
		end if
		break

	case MZEGGMAN_DEFEATRISE
		if object.timer < 40
			object.ypos += object.yvel
			object.yvel -= 0x800
			object.timer++
		else
			object.timer = 0
			object.yvel = 0
			object.angle = 0
			object.originPos.y = object.ypos
			object.state++
		end if
		break

	case MZEGGMAN_FLEE
		CallFunction(MZEggman_Oscillate)

		if object.timer < 8
			object.timer++
		else
			object.timer = 0
			object.animation = MZEGGANI_PANIC
			object.animationTimer = 0
			object.flameAnim = FLAME_EXPLODE
			object.flameAnimTimer = 0
			object.priority = PRIORITY_ACTIVE
			temp0 = tileLayer[0].xsize
			temp0 <<= 7
			stage.newXBoundary2 = temp0
			object.state++
		end if
		break

	case MZEGGMAN_ESCAPE
		CallFunction(MZEggman_Oscillate)
		object.xpos += 0x40000
		object.originPos.y -= 0x4000
		if object.outOfBounds == true
			object.type = TypeName[Blank Object]
			object.priority = PRIORITY_BOUNDS
		end if
		break

	case MZEGGMAN_HITFREEZE
		object.hitDelay++
		if object.hitDelay == 16
			object.hitDelay = 0
			object.state = object.nextState
		end if
		break
	end switch

	if object.health != 0
		if object.invincibilityTimer > 0
			object.invincibilityTimer--
		end if

		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			if object.invincibilityTimer == 0
				CheckEqual(player[currentPlayer].animation, ANI_HURT)
				temp0 = checkResult
				CheckEqual(player[currentPlayer].animation, ANI_DYING)
				temp0 |= checkResult
				CheckEqual(player[currentPlayer].animation, ANI_DROWNING)
				temp0 |= checkResult
				if temp0 != false
					if object.animation != MZEGGANI_LAUGH
						object.animation = MZEGGANI_LAUGH
						object.animationTimer = 0
					end if
				end if

				BoxCollisionTest(C_TOUCH, object.entityPos, -20, -16, 20, 16, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
				if checkResult == true
					CallFunction(Player_CheckHit)
					if object.state > MZEGGMAN_TAUNT
						if object.state != MZEGGMAN_HITFREEZE
							object.nextState = object.state
							object.state = MZEGGMAN_HITFREEZE
						end if
					end if

					if checkResult == true
						object.health--
						if object.health == 0
#platform: USE_ORIGINS
							CallNativeFunction2(NotifyCallback, NOTIFY_KILL_BOSS, 0)
							CallNativeFunction2(NotifyCallback, NOTIFY_BOSS_END, true)
#endplatform
							player.score += 1000
							object.drawOrder = 3
							object.animation = MZEGGANI_DEFEATED
							object.animationTimer = 0
							object.flameAnim = FLAME_INACTIVE
							object.timer = 0
							object.yvel = 0
							object.state = MZEGGMAN_EXPLODE
						else
							object.animation = MZEGGANI_HIT
							object.animationTimer = 0
							object.invincibilityTimer = 32
							PlaySfx(SfxName[Boss Hit], false)
						end if
					end if
				end if
			end if
		next
	end if

	// Handle Right Bounds
	if object.state < MZEGGMAN_ESCAPE
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			temp0 = player[currentPlayer].collisionRight
			temp0 <<= 16
			temp0 += player[currentPlayer].xpos
			temp1 = stage.curXBoundary2
			temp1 <<= 16
			if temp0 > temp1
				player[currentPlayer].xvel = 0
				player[currentPlayer].speed = 0
				player[currentPlayer].xpos = temp1
				temp0 = player[currentPlayer].collisionRight
				temp0 <<= 16
				player[currentPlayer].xpos -= temp0
			end if
		next
	end if
end event


event ObjectDraw
	switch object.animation
	default
	case MZEGGANI_INVISIBLE
		break

	case MZEGGANI_IDLE
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 3
			object.eggmanFrame = 3
		end if
		if object.eggmanFrame > 11
			object.eggmanFrame = 3
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(212, FX_FLIP, object.xpos, object.ypos)
		break

	case MZEGGANI_LAUGH
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 12
			object.eggmanFrame = 12
		end if
		if object.eggmanFrame > 108
			object.animation = MZEGGANI_IDLE
		end if
		if object.eggmanFrame > 107
			if object[0].animation != ANI_DYING
				object.animation = MZEGGANI_IDLE
			end if
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(212, FX_FLIP, object.xpos, object.ypos)
		break

	case MZEGGANI_LAUGH_LOOP
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 12
			object.eggmanFrame = 12
		end if
		if object.eggmanFrame > 102
			object.eggmanFrame = 43
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(212, FX_FLIP, object.xpos, object.ypos)
		break

	case MZEGGANI_HIT
		object.animationTimer++
		object.eggmanFrame++
		object.mobileAnimTimer++
		object.equipmentAnimTimer++
		object.flameAnim = FLAME_ACTIVE
		if object.equipmentAnimTimer > 1
			object.equipmentFrame++
			object.equipmentAnimTimer = 0
		end if
		if object.mobileAnimTimer > 1
			object.mobileFrame++
			object.mobileAnimTimer = 0
		end if
		if object.mobileFrame < 0
			object.mobileFrame = 0
		end if
		if object.mobileFrame > 1
			object.mobileFrame = 0
		end if
		if object.equipmentFrame < 212
			object.equipmentFrame = 212
		end if
		if object.equipmentFrame > 213
			object.equipmentFrame = 212
		end if
		if object.eggmanFrame < 109
			object.eggmanFrame = 109
		end if
		if object.eggmanFrame > 139
			object.eggmanFrame = 109
		end if
		if object.animationTimer == 30
			object.animationTimer = 0
			object.animation = MZEGGANI_IDLE
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.mobileFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.equipmentFrame, FX_FLIP, object.xpos, object.ypos)
		break

	case MZEGGANI_DEFEATED
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 140
			object.eggmanFrame = 140
		end if
		if object.eggmanFrame > 175
			object.eggmanFrame = 140
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(212, FX_FLIP, object.xpos, object.ypos)
		break

	case MZEGGANI_TOASTED
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 140
			object.eggmanFrame = 140
		end if
		if object.eggmanFrame > 175
			object.eggmanFrame = 140
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(212, FX_FLIP, object.xpos, object.ypos)
		break

	case MZEGGANI_PANIC
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 176
			object.eggmanFrame = 176
		end if
		if object.eggmanFrame > 193
			object.eggmanFrame = 176
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(212, FX_FLIP, object.xpos, object.ypos)
		break
		
	end switch

	switch object.flameAnim
	default
	case FLAME_INACTIVE
		break

	case FLAME_ACTIVE
		object.flameAnimTimer++
		if object.flameAnimTimer > 1
			object.flameFrame++
			object.flameAnimTimer = 0
		end if
		if object.flameFrame < 194
			object.flameFrame = 194
		end if
		if object.flameFrame > 198
			object.flameFrame = 194
		end if
		DrawSpriteFX(object.flameFrame, FX_FLIP, object.xpos, object.ypos)
		break

	case FLAME_EXPLODE
		object.flameAnimTimer++
		if object.flameAnimTimer > 1
			object.flameFrame++
			object.flameAnimTimer = 0
		end if
		if object.flameFrame < 199
			object.flameFrame = 199
		end if
		if object.flameFrame > 211
			object.flameFrame = 207
		end if
		DrawSpriteFX(object.flameFrame, FX_FLIP, object.xpos, object.ypos)
		break
		
	end switch
end event


event ObjectStartup
	switch stage.playerListPos
	case PLAYER_SONIC_A
		LoadSpriteSheet("Global/Eggman.gif")
		break

	case PLAYER_TAILS_A
		LoadSpriteSheet("Global/Eggman.gif")
		break

	case PLAYER_KNUCKLES_A
		LoadSpriteSheet("Global/EggRobo.gif")
		break

	case PLAYER_AMY_A
		LoadSpriteSheet("Global/Eggman.gif")
		break
	end switch
	// Empty Egg Mobile
	SpriteFrame(-28, -32, 64, 56, 320, 1) // Frame 0
	SpriteFrame(-28, -32, 64, 56, 320, 58) // Frame 1

	// Egg Mobile Seat
	SpriteFrame(-28, -32, 64, 24, 128, 231) // Frame 2

	// Eggman Idle
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 3
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 4
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 5
	SpriteFrame(-28, -32, 64, 24, 1, 25) // Frame 6
	SpriteFrame(-28, -32, 64, 24, 1, 25) // Frame 7
	SpriteFrame(-28, -32, 64, 24, 1, 25) // Frame 8
	SpriteFrame(-28, -32, 64, 24, 1, 50) // Frame 9
	SpriteFrame(-28, -32, 64, 24, 1, 50) // Frame 10
	SpriteFrame(-28, -32, 64, 24, 1, 50) // Frame 11

	//Eggman Laugh
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 12
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 13

	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 14
	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 15
	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 16
	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 17

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 18
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 19
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 20
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 21

	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 22
	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 23
	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 24
	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 25

	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 26
	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 27
	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 28
	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 29

	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 30
	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 31
	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 32
	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 33

	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 34
	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 35
	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 36
	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 37

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 38
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 39
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 40
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 41
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 42

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 43
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 44
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 45
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 46
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 47

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 48
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 49
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 50
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 51
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 52

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 53
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 54
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 55
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 56
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 57

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 58
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 59
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 60
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 61
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 62

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 63
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 64
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 65
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 66
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 67

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 68
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 69
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 70
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 71
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 72

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 73
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 74
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 75
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 76
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 77

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 78
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 79
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 80
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 81
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 82

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 83
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 84
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 85
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 86
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 87

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 88
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 89
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 90
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 91
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 92

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 93
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 94
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 95
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 96
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 97

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 98
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 99
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 10
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 101
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 102

	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 103
	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 104
	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 105
	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 106

	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 107
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 108

	// Eggman Hurt
	SpriteFrame(-28, -32, 64, 24, 66, 25) // Frame 109
	SpriteFrame(-28, -32, 64, 24, 66, 25) // Frame 110

	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 111
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 112

	SpriteFrame(-28, -32, 64, 24, 66, 75) // Frame 113
	SpriteFrame(-28, -32, 64, 24, 66, 75) // Frame 114

	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 115
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 116

	SpriteFrame(-28, -32, 64, 24, 66, 100) // Frame 117
	SpriteFrame(-28, -32, 64, 24, 66, 100) // Frame 118

	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 119
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 120

	SpriteFrame(-28, -32, 64, 24, 66, 125) // Frame 121
	SpriteFrame(-28, -32, 64, 24, 66, 125) // Frame 122

	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 123
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 124
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 125

	SpriteFrame(-28, -32, 64, 24, 66, 150) // Frame 126
	SpriteFrame(-28, -32, 64, 24, 66, 150) // Frame 127
	SpriteFrame(-28, -32, 64, 24, 66, 150) // Frame 128

	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 129
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 130
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 131

	SpriteFrame(-28, -32, 64, 24, 64, 175) // Frame 132
	SpriteFrame(-28, -32, 64, 24, 64, 175) // Frame 133
	SpriteFrame(-28, -32, 64, 24, 64, 175) // Frame 134
	SpriteFrame(-28, -32, 64, 24, 64, 175) // Frame 135
	SpriteFrame(-28, -32, 64, 24, 64, 175) // Frame 136
	SpriteFrame(-28, -32, 64, 24, 64, 175) // Frame 137

	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 138
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 139

	// Eggman Toasted
	SpriteFrame(-28, -32, 64, 24, 64, 200) // Frame 140
	SpriteFrame(-28, -32, 64, 24, 64, 200) // Frame 141
	SpriteFrame(-28, -32, 64, 24, 64, 200) // Frame 142
	SpriteFrame(-28, -32, 64, 24, 64, 200) // Frame 143
	SpriteFrame(-28, -32, 64, 24, 64, 200) // Frame 144
	SpriteFrame(-28, -32, 64, 24, 64, 200) // Frame 145
	SpriteFrame(-28, -32, 64, 24, 131, 58) // Frame 146
	SpriteFrame(-28, -32, 64, 24, 131, 58) // Frame 147
	SpriteFrame(-28, -32, 64, 24, 131, 58) // Frame 148
	SpriteFrame(-28, -32, 64, 24, 131, 58) // Frame 149
	SpriteFrame(-28, -32, 64, 24, 131, 58) // Frame 150
	SpriteFrame(-28, -32, 64, 24, 131, 58) // Frame 151
	SpriteFrame(-28, -32, 64, 24, 131, 83) // Frame 152
	SpriteFrame(-28, -32, 64, 24, 131, 83) // Frame 153
	SpriteFrame(-28, -32, 64, 24, 131, 83) // Frame 154
	SpriteFrame(-28, -32, 64, 24, 131, 83) // Frame 155
	SpriteFrame(-28, -32, 64, 24, 131, 83) // Frame 156
	SpriteFrame(-28, -32, 64, 24, 131, 83) // Frame 157
	SpriteFrame(-28, -32, 64, 24, 130, 151) // Frame 158
	SpriteFrame(-28, -32, 64, 24, 130, 151) // Frame 159
	SpriteFrame(-28, -32, 64, 24, 130, 151) // Frame 160
	SpriteFrame(-28, -32, 64, 24, 130, 151) // Frame 161
	SpriteFrame(-28, -32, 64, 24, 130, 151) // Frame 162
	SpriteFrame(-28, -32, 64, 24, 130, 151) // Frame 163
	SpriteFrame(-28, -32, 64, 24, 255, 1) // Frame 164
	SpriteFrame(-28, -32, 64, 24, 255, 1) // Frame 165
	SpriteFrame(-28, -32, 64, 24, 255, 1) // Frame 166
	SpriteFrame(-28, -32, 64, 24, 255, 1) // Frame 167
	SpriteFrame(-28, -32, 64, 24, 255, 1) // Frame 168
	SpriteFrame(-28, -32, 64, 24, 255, 1) // Frame 169
	SpriteFrame(-28, -32, 64, 24, 255, 26) // Frame 170
	SpriteFrame(-28, -32, 64, 24, 255, 26) // Frame 171
	SpriteFrame(-28, -32, 64, 24, 255, 26) // Frame 172
	SpriteFrame(-28, -32, 64, 24, 255, 26) // Frame 173
	SpriteFrame(-28, -32, 64, 24, 255, 26) // Frame 174
	SpriteFrame(-28, -32, 64, 24, 255, 26) // Frame 175

	// Eggman Sweat
	SpriteFrame(-28, -32, 64, 24, 255, 51) // Frame 176
	SpriteFrame(-28, -32, 64, 24, 255, 51) // Frame 177
	SpriteFrame(-28, -32, 64, 24, 255, 51) // Frame 178
	SpriteFrame(-28, -32, 64, 24, 255, 76) // Frame 179
	SpriteFrame(-28, -32, 64, 24, 255, 76) // Frame 180
	SpriteFrame(-28, -32, 64, 24, 255, 76) // Frame 181
	SpriteFrame(-28, -32, 64, 24, 255, 101) // Frame 182
	SpriteFrame(-28, -32, 64, 24, 255, 101) // Frame 183
	SpriteFrame(-28, -32, 64, 24, 255, 101) // Frame 184
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 185
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 186
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 187
	SpriteFrame(-28, -32, 64, 24, 1, 25) // Frame 188
	SpriteFrame(-28, -32, 64, 24, 1, 25) // Frame 189
	SpriteFrame(-28, -32, 64, 24, 1, 25) // Frame 190
	SpriteFrame(-28, -32, 64, 24, 1, 50) // Frame 191
	SpriteFrame(-28, -32, 64, 24, 1, 50) // Frame 192
	SpriteFrame(-28, -32, 64, 24, 1, 50) // Frame 193

	// Jet
	SpriteFrame(34, 1, 10, 9, 196, 2) // Frame 194
	SpriteFrame(33, 0, 15, 11, 196, 12) // Frame 195
	SpriteFrame(33, 1, 10, 9, 196, 52) // Frame 196
	SpriteFrame(33, 1, 13, 9, 196, 105) // Frame 197
	SpriteFrame(33, 0, 25, 11, 210, 104) // Frame 198

	// Jet Burst
	SpriteFrame(34, -1, 22, 14, 213, 1) // Frame 199
	SpriteFrame(34, -1, 22, 14, 213, 1) // Frame 200
	SpriteFrame(34, -8, 32, 26, 213, 16) // Frame 201
	SpriteFrame(34, -8, 32, 26, 213, 16) // Frame 202
	SpriteFrame(34, -8, 32, 26, 205, 169) // Frame 203
	SpriteFrame(34, -8, 32, 26, 205, 169) // Frame 204
	SpriteFrame(34, -8, 32, 26, 205, 196) // Frame 205
	SpriteFrame(34, -1, 22, 14, 213, 1) // Frame 206
	SpriteFrame(34, 1, 10, 9, 196, 2) // Frame 207
	SpriteFrame(33, 0, 15, 11, 196, 12) // Frame 208
	SpriteFrame(33, 1, 10, 9, 196, 52) // Frame 209
	SpriteFrame(33, 1, 13, 9, 196, 105) // Frame 210
	SpriteFrame(33, 0, 25, 11, 210, 104) // Frame 211

	// Fireball Shooter
	SpriteFrame(-8, 16, 16, 16, 238, 43) // Frame 212
	SpriteFrame(-8, 16, 16, 16, 238, 135) // Frame 213

	// Set up values for every Eggman object
	foreach (TypeName[Eggman], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].animation = MZEGGANI_IDLE
#platform: USE_STANDALONE
		object[arrayPos0].health = 8
#endplatform
#platform: USE_ORIGINS
		if game.bossOneLife == false
			object[arrayPos0].health = 8
		else
			object[arrayPos0].health = 1
		end if
#endplatform
	next

	if stage.actNum == 3
		if screen.xsize == 320
			// Change the boss arena to match the original game's version
			SetTileLayerEntry(348, 0, 50, 5)
			SetTileLayerEntry(349, 0, 51, 5)
		end if
	end if
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
	DrawSprite(19)
end event


event RSDKLoad
	LoadSpriteSheet("Global/Eggman.gif")
	SpriteFrame(-28, -32, 64, 56, 1, 1)
	SpriteFrame(-28, -32, 64, 56, 66, 1)
	SpriteFrame(-28, -32, 64, 56, 131, 1)
	SpriteFrame(-28, -32, 64, 56, 1, 58)
	SpriteFrame(-28, -32, 64, 56, 1, 1)
	SpriteFrame(-28, -32, 64, 56, 1, 58)
	SpriteFrame(-28, -32, 64, 56, 66, 58)
	SpriteFrame(-28, -32, 64, 56, 131, 58)
	SpriteFrame(-28, -32, 64, 56, 66, 1)
	SpriteFrame(-28, -32, 64, 56, 1, 115)
	SpriteFrame(-28, -32, 64, 56, 66, 115)
	SpriteFrame(34, 1, 10, 9, 196, 2)
	SpriteFrame(33, 0, 15, 11, 196, 12)
	SpriteFrame(34, -1, 22, 14, 213, 1)
	SpriteFrame(34, -8, 32, 26, 213, 16)
	SpriteFrame(34, -1, 22, 14, 213, 1)
	SpriteFrame(34, -8, 32, 26, 213, 16)
	SpriteFrame(34, 1, 10, 9, 196, 2)
	SpriteFrame(33, 0, 15, 11, 196, 12)
	SpriteFrame(-8, 16, 16, 16, 238, 43)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
