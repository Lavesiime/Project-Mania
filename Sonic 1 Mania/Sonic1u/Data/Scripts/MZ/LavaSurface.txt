// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Lava Surface Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.propertyValue : object.length

private alias object.value3 : object.startPos
private alias object.value4 : object.endPos

private alias object.value9 : object.timer
private alias object.value10 : object.playerID

// Player Aliases
private alias object.state : player.state
private alias object.xpos  : player.xpos
private alias object.ypos : player.ypos

private alias object.value37 : player.shield


private alias object.value17 : debugMode.currentSelection

// Tile Flag Aliases
private alias 1 : TILE_LAVA

// Shields
private alias 3 : SHIELD_FIRE

// MZSetup aliases
private alias object.value3 : mzSetup.lavaAnimTimer

// Reserved object slots
private alias 10 : SLOT_ZONESETUP


// ========================
// Function Declarations
// ========================

reserve function LavaSurface_DebugDraw
reserve function LavaSurface_DebugSpawn


// ========================
// Static Values
// ========================

private value LavaSurface_startDebugID = 0

// ========================
// Function Definitions
// ========================

private function LavaSurface_DebugDraw
	temp0 = debugMode[0].currentSelection
	temp0 -= LavaSurface_startDebugID
	temp2 = temp0
	temp0 <<= 20
	FlipSign(temp0)
	temp0 += object.xpos
	temp0 += 0x100000

	temp1 = 0
	while temp1 < temp2
		DrawSpriteXY(object.frame, temp0, object.ypos)
		temp0 += 0x200000
		temp1++
	loop

	if temp1 == 0
		// If no sprites are drawn, then draw at least 1
		DrawSprite(object.frame)
	end if
end function


private function LavaSurface_DebugSpawn
	temp0 = debugMode[0].currentSelection
	temp0 -= LavaSurface_startDebugID
	CreateTempObject(TypeName[Lava Surface], temp0, object.xpos, object.ypos)

	object[tempObjectPos].startPos = temp0
	object[tempObjectPos].startPos <<= 19
	object[tempObjectPos].endPos = object[tempObjectPos].startPos
	FlipSign(object[tempObjectPos].startPos)
	object[tempObjectPos].startPos += object[tempObjectPos].xpos
	object[tempObjectPos].endPos += object[tempObjectPos].xpos
	object[tempObjectPos].playerID = -1
end function



// ========================
// Events
// ========================

event ObjectUpdate
	object.animationTimer++
	if object.animationTimer >= 20
		object.frame++
		object.animationTimer = 0
	end if
	if object.frame > 2
		object.frame = 0
	end if
	object.drawOrder = 5
	temp0 = object.propertyValue
	temp0 <<= 4
	temp1 = temp0
	FlipSign(temp0)
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, temp0, 7, temp1, 5, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
		if checkResult == true
			CallFunction(Player_FireHit)
		end if
	next

end event


event ObjectDraw

	temp0 = object.propertyValue
	temp2 = temp0
	temp0 <<= 20
	FlipSign(temp0)
	temp0 += object.xpos
	temp0 += 0x100000

	temp1 = 0
	while temp1 < temp2
		DrawSpriteXY(object.frame, temp0, object.ypos)
		temp0 += 0x200000
		temp1++
	loop
	if temp1 == 0
		// If no sprites are drawn, then draw at least 1
		DrawSprite(object.frame)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("MZ/Objects.gif")
	SpriteFrame(-16, -8, 32, 16, 1, 177)
	SpriteFrame(-16, -8, 32, 16, 34, 177)
	SpriteFrame(-16, -8, 32, 16, 474, 493)

	foreach (TypeName[LavaSurface], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].startPos = object[arrayPos0].length
		object[arrayPos0].startPos <<= 19
		object[arrayPos0].endPos = object[arrayPos0].startPos
		FlipSign(object[arrayPos0].startPos)
		object[arrayPos0].startPos += object[arrayPos0].xpos
		object[arrayPos0].endPos += object[arrayPos0].xpos
		object[arrayPos0].playerID = -1
	next

	// Add bridges of varying lengths to the debug object list
	temp0 = 0
	LavaSurface_startDebugID = DebugMode_ObjCount
	LavaSurface_startDebugID--
	while temp0 < 16
		SetTableValue(TypeName[LavaSurface], DebugMode_ObjCount, DebugMode_TypesTable)
		SetTableValue(LavaSurface_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
		SetTableValue(LavaSurface_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
		DebugMode_ObjCount++
		temp0++
	loop
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
	DrawSprite(1)
	DrawSprite(2)
	DrawSprite(3)
	DrawSprite(object.frame)
end event


event RSDKLoad
	LoadSpriteSheet("MZ/Objects.gif")
	SpriteFrame(-128, -32, 128, 32, 1, 405)
	SpriteFrame(0, -32, 128, 32, 1, 405)
	SpriteFrame(-128, 0, 256, 32, 1, 405)
	SpriteFrame(0, 0, 256, 32, 1, 405)
	SpriteFrame(124, -32, 60, 64, 1, 194)
	SpriteFrame(124, -32, 60, 64, 62, 194)
	SpriteFrame(124, -32, 60, 64, 1, 259)
	SpriteFrame(124, -32, 60, 64, 62, 259)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
