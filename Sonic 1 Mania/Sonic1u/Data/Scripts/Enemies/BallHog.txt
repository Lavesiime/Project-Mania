// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Ball Hog Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value1 : object.timer

private alias object.value0 : ballHogBomb.bombTime

// Player aliases
private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom

// Path ID Aliases
private alias 0 : PATH_A


// ========================
// Function Declarations
// ========================

reserve function BallHog_DebugDraw
reserve function BallHog_DebugSpawn


// ========================
// Tables
// ========================

private table BallHog_frameTable
	0,8,9,10,11,11,11,14,11,10,9,8,0,8,9,10,11,11,11,14,11,10,9,8,0,8,9,10,11,11,11,14,11,10,9,8,0,1,2,3,4,3,2,0
end table

private table BallHog_frameDelayTable
	4,10,4,4,4,5,5,5,4,4,4,10,4,4,4,5,5,5,4,4,4,10,4,4,4,5,5,5,4,4,4,10,4,3,3,4,4,3,3,4,5
end table


// ========================
// Function Definitions
// ========================

private function BallHog_DebugDraw
	DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
end function


private function BallHog_DebugSpawn
	CreateTempObject(TypeName[Ball Hog], 0, object.xpos, object.ypos)
	object[tempObjectPos].direction = object.direction
	object[tempObjectPos].propertyValue = 1
end function


// ========================
// Events
// ========================

event ObjectUpdate

	// Check collision with players
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, -12, -18, 12, 18, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
		if checkResult == true
			CallFunction(Player_BadnikBreak)
		end if
	next

	// Sidestep a little
	if object.frame > 11
		if object.frame < 15
			object.ypos += object.yvel
			object.yvel += 0x95C0
			ObjectTileCollision(CSIDE_FLOOR, 0, 20, PATH_A)
			if checkResult == false
				object.animationTimer = 2
			end if
		end if
	end if

	// Update animation & ball lobbing
	object.animationTimer--
	if object.animationTimer <= 0

		GetTableValue(object.animationTimer, object.timer, BallHog_frameDelayTable)
		GetTableValue(object.frame, object.timer, BallHog_frameTable)

		object.timer++
		if object.timer >= 50
			object.timer = 0
		end if

		if object.frame == 14
			object.yvel = -0x29999
			object.ypos += object.yvel
			object.yvel += 0x95C0
			PlaySfx(SfxName[Hog Jump], false)
		end if

		if object.frame == 4
			// Drop a bomb ball

			CreateTempObject(TypeName[Ball Hog Bomb], 0, object.xpos, object.ypos)

			// Make the bomb match the hog's direction
			if object.direction == 0
				object[tempObjectPos].xpos -= 0x40000
				object[tempObjectPos].xvel = -0x10000
			else
				object[tempObjectPos].xpos += 0x40000
				object[tempObjectPos].xvel = 0x10000
			end if

			object[tempObjectPos].ypos += 0xC0000
			ballHogBomb[tempObjectPos].bombTime = object.propertyValue
			ballHogBomb[tempObjectPos].bombTime *= 60
			
			PlaySfx(SfxName[Hog Drop], false)

		end if
	end if
end event


event ObjectDraw
	DrawSpriteFX(object.frame, FX_FLIP, object.xpos, object.ypos)
end event


event ObjectStartup
	CheckCurrentStageFolder("Zone06")
	if checkResult == true
		LoadSpriteSheet("SBZ/Objects.gif")
		//Idle Frames
		SpriteFrame(-11, -17, 22, 37, 1, 163) // Frame 0
		SpriteFrame(-11, -17, 22, 37, 72, 201) // Frame 1

		// Open/Close Frames
		SpriteFrame(-11, -17, 22, 37, 49, 201) // Frame 2
		SpriteFrame(-11, -17, 22, 37, 219, 350) // Frame 3
		SpriteFrame(-11, -17, 22, 37, 24, 163) // Frame 4
		SpriteFrame(-11, -17, 22, 37, 219, 350) // Frame 5
		SpriteFrame(-11, -17, 22, 37, 49, 201) // Frame 6
		SpriteFrame(-11, -17, 22, 37, 1, 163) // Frame 7

		// Jump Frames
		SpriteFrame(-11, -17, 22, 37, 95, 201) // Frame 8
		SpriteFrame(-12, -17, 24, 37, 0, 201) // Frame 9
		SpriteFrame(-12, -17, 24, 37, 47, 163) // Frame 10
		SpriteFrame(-12, -17, 24, 37, 24, 201) // Frame 11
		SpriteFrame(-11, -17, 22, 41, 72, 159) // Frame 12
		SpriteFrame(-11, -12, 22, 37, 95, 163) // Frame 13
		SpriteFrame(-13, -12, 24, 36, 79, 415) // Frame 14
	end if

	CheckCurrentStageFolder("Zone07")
	if checkResult == true
		LoadSpriteSheet("MBZ/Objects.gif")
		SpriteFrame(-11, -17, 22, 37, 76, 292)
		SpriteFrame(-11, -17, 22, 37, 99, 292)
		SpriteFrame(-12, -12, 24, 32, 122, 297)
		SpriteFrame(-11, -20, 22, 40, 147, 289)
	end if

	foreach (TypeName[Ball Hog], arrayPos0, ALL_ENTITIES)
		temp0 = object[arrayPos0].propertyValue
		temp0 &= 128
		if temp0 != 0
			object[arrayPos0].direction = FACING_LEFT
			object[arrayPos0].propertyValue &= 127
		end if
	next

	SetTableValue(TypeName[Ball Hog], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(BallHog_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(BallHog_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKEdit
	if editor.returnVariable == true
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			checkResult = object.propertyValue
			break
			
		case 0 // bombTime
			checkResult = object.propertyValue
			checkResult &= 0x7F
			break
			
		case 1 // direction
			checkResult = object.propertyValue
			checkResult &= 0x80
			CheckGreater(checkResult, 0x7F)
			break
			
		end switch
	else
		switch editor.variableID
		case EDIT_VAR_PROPVAL // property value
			object.propertyValue = editor.variableValue
			break
			
		case 0 // bombTime
			object.propertyValue &= 0x80
			object.propertyValue |= editor.variableValue
			break
			
		case 1 // direction
			object.propertyValue &= 0x7F

			temp0 = editor.variableValue
			temp0 <<= 7
			object.propertyValue |= temp0
			break
			
		end switch
	end if
end event


event RSDKDraw
	temp0 = object.propertyValue
	temp0 &= 0x80
	object.direction = FACING_RIGHT
	if temp0 != false
		object.direction = FACING_LEFT
	end if

	DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
end event


event RSDKLoad
	CheckCurrentStageFolder("Zone06")
	if checkResult == true
		LoadSpriteSheet("SBZ/Objects.gif")
		SpriteFrame(-11, -17, 22, 37, 1, 170)
		SpriteFrame(-11, -17, 22, 37, 24, 170)
		SpriteFrame(-12, -12, 24, 32, 47, 175)
		SpriteFrame(-11, -20, 22, 40, 72, 167)
	end if
	
	CheckCurrentStageFolder("Zone07")
	if checkResult == true
		LoadSpriteSheet("MBZ/Objects.gif")
		SpriteFrame(-11, -17, 22, 37, 76, 292)
		SpriteFrame(-11, -17, 22, 37, 99, 292)
		SpriteFrame(-12, -12, 24, 32, 122, 297)
		SpriteFrame(-11, -20, 22, 40, 147, 289)
	end if

	AddEditorVariable("bombTime")

	AddEditorVariable("direction")
	SetActiveVariable("direction")
	AddEnumVariable("Left", 0)
	AddEnumVariable("Right", 1)
end event
