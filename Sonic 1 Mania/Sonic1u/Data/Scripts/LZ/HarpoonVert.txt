// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Harpoon V Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer
private alias object.value1 : object.hitboxT
private alias object.value2 : object.hitboxB
private alias object.value10 : object.smoothMovement


// ========================
// Function Declarations
// ========================

reserve function HarpoonV_DebugDraw
reserve function HarpoonV_DebugSpawn


// ========================
// Tables
// ========================

private table HarpoonV_frameTable
	0, 1, 2, 1
end table

private table HarpoonV_delayTable
	64, 6, 64, 6
end table

private table HarpoonV_sizeTable
	-8, -24, -40, -24
end table


// ========================
// Function Definitions
// ========================

private function HarpoonV_DebugDraw
	temp0 = object.direction
	if object.direction == FLIP_X
		object.direction = FLIP_Y
	end if
	DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
	object.direction = temp0
end function


private function HarpoonV_DebugSpawn
	CreateTempObject(TypeName[Harpoon V], 0, object.xpos, object.ypos)
	object[tempObjectPos].direction = object.direction
	object[tempObjectPos].direction <<= 1
end function


// ========================
// Events
// ========================

event ObjectUpdate
	if object.animationTimer == 0
		if object.animationTimer == 3
			//PlaySfx(SfxName[Spear], false) Gonna need to look into this
		end if
		object.timer++
		if object.timer >= 4
			object.timer = 0
		end if
		GetTableValue(object.animationTimer, object.timer, HarpoonV_delayTable)
		GetTableValue(object.frame, object.timer, HarpoonV_frameTable)
	else
		if object.value6 == 0
			object.animationTimer--
			if object.animationTimer == 3
				//PlaySfx(SfxName[Spear], false) Gonna need to look into this
			end if
			object.value6 = 0
		else
			object.value6--
		end if
	end if

	GetTableValue(object.hitboxT, object.timer, HarpoonV_sizeTable)
	object.hitboxB = 8
	GetBit(temp2, object.direction, 1)
	if temp2 == true // Is FLIP_Y set?
		FlipSign(object.hitboxT)
		FlipSign(object.hitboxB)
		temp0 = object.hitboxB
		object.hitboxB = object.hitboxT
		object.hitboxT = temp0
	end if

	// Check interaction with players
	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, -4, object.hitboxT, 4, object.hitboxB, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
		if checkResult == true
			CallFunction(Player_Hit)
		end if
	next
end event


event ObjectDraw
	object.smoothMovement = true
	
	if object.smoothMovement == true
		temp0 = object.ypos
		if object.timer == 0
			GetBit(temp2, object.direction, 1)
			if temp2 == true // Is FLIP_Y set?
				temp0 -= 0x180000
			else
				temp0 += 0x180000
			end if
		end if
		if object.timer == 1
			temp1 = 0x040000
			temp3 = object.animationTimer
			temp3 += 1
			temp1 *= temp3
			GetBit(temp2, object.direction, 1)
			if temp2 == true // Is FLIP_Y set?
				temp0 -= temp1
				temp0 += 0x040000
			else
				temp0 += temp1
				temp0 -= 0x040000
			end if
		end if
		if object.timer == 3
			temp1 = 0x040000
			temp3 = object.animationTimer
			temp3 += 1
			temp1 *= temp3
			GetBit(temp2, object.direction, 1)
			if temp2 == true // Is FLIP_Y set?
				temp0 -= 0x1C0000
				temp0 += temp1
			else
				temp0 += 0x1C0000
				temp0 -= temp1
			end if
		end if
		
		DrawSpriteFX(3, FX_FLIP, object.xpos, temp0)
		DrawSpriteFX(4, FX_FLIP, object.xpos, object.ypos)
	else
		DrawSpriteFX(object.frame, FX_FLIP, object.xpos, object.ypos)
	end if
end event


event ObjectStartup
	LoadSpriteSheet("LZ/Objects2.gif")
	
	// Harpoon frames
	SpriteFrame(-4, -8, 8, 16, 231, 239)
	SpriteFrame(-4, -24, 8, 32, 222, 223)
	SpriteFrame(-4, -40, 8, 48, 213, 207)
	
	SpriteFrame(-4, -37, 8, 42, 242, 330) // Smooth movement frames
	SpriteFrame(-4, 2, 8, 6, 242, 373)

	// Add this object to the debug item list
	SetTableValue(TypeName[Harpoon V], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(HarpoonV_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(HarpoonV_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
end event


event RSDKLoad
	LoadSpriteSheet("LZ/Objects.gif")
	SpriteFrame(-4, -40, 8, 48, 213, 207)
	
	// dir is set from scene
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
