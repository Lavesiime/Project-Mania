// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Eggman Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0  : object.timer
private alias object.value1  : object.health
private alias object.value2  : object.invincibilityTimer
private alias object.value3  : object.flameAnim
private alias object.value4  : object.flameAnimTimer
private alias object.value5  : object.boundsL
private alias object.value6  : object.boundsR
private alias object.value7  : object.boundsB
private alias object.value8  : object.originPos.y
private alias object.value9  : object.stingerPos
private alias object.value10 : object.grabbedBlockSlot
private alias object.value11 : object.playerDetected
private alias object.value12 : object.eggmanFrame
private alias object.value13 : object.mobileAnimTimer
private alias object.value14 : object.flameFrame
private alias object.value15 : object.equipmentFrame
private alias object.value16 : object.equipmentAnimTimer

// Marker Aliases
private alias object.xpos  : bossMarker.xpos
private alias object.ypos  : bossMarker.ypos
private alias object.ixpos : bossMarker.ixpos
private alias object.iypos : bossMarker.iypos

// Eggman States
private alias 0  : SYZEGGMAN_AWAITPLAYER
private alias 1  : SYZEGGMAN_MOVELEFT
private alias 2  : SYZEGGMAN_MOVERIGHT
private alias 3  : SYZEGGMAN_LOWERDOWN
private alias 4  : SYZEGGMAN_GRABBLOCK
private alias 5  : SYZEGGMAN_GRABBEDBLOCK
private alias 6  : SYZEGGMAN_SHAKEBLOCK
private alias 7  : SYZEGGMAN_RISEUP
private alias 8  : SYZEGGMAN_DESTROYBLOCK
private alias 9  : SYZEGGMAN_RETRACTSTINGER
private alias 10 : SYZEGGMAN_MISSEDBLOCK
private alias 11 : SYZEGGMAN_EXPLODE
private alias 12 : SYZEGGMAN_DEFEATED
private alias 13 : SYZEGGMAN_FLEE
private alias 14 : SYZEGGMAN_ESCAPE

// Eggman Animations
private alias 0 : SYZEGGANI_INVISIBLE
private alias 1 : SYZEGGANI_IDLE
private alias 2 : SYZEGGANI_LAUGH
private alias 3 : SYZEGGANI_LAUGH_LOOP
private alias 4 : SYZEGGANI_HIT
private alias 5 : SYZEGGANI_DEFEATED
private alias 6 : SYZEGGANI_TOASTED
private alias 7 : SYZEGGANI_PANIC

// Flame Animations
private alias 0 : FLAME_INACTIVE
private alias 1 : FLAME_ACTIVE
private alias 2 : FLAME_EXPLODE

// Boss Barrier Aliases
private alias object.state : bossBarrier.state

private alias 1 : BOSSBARRIER_RISE
private alias 2 : BOSSBARRIER_LOWER

// Player Aliases
private alias object.xpos : player.xpos
private alias object.xvel : player.xvel
private alias object.speed : player.speed
private alias object.animation : player.animation
private alias object.collisionRight : player.collisionRight

private alias object.value40 : player.hitboxLeft
private alias object.value38 : player.hitboxTop
private alias object.value41 : player.hitboxRight
private alias object.value39 : player.hitboxBottom

// Music Events
private alias 26 : SLOT_MUSICEVENT_BOSS

private alias 0 : MUSICEVENT_FADETOBOSS
private alias 1 : MUSICEVENT_FADETOSTAGE
private alias 2 : MUSICEVENT_TRANSITION

// ========================
// Function Declarations
// ========================

reserve function SYZEggman_Float
reserve function SYZEggman_CheckAbovePlayer
reserve function SYZEggman_HandleBlockMovement

// Using SYZEggman as a prefix here because there's like 12 "Eggman" objects in the game so its less confusing this way

private function SYZEggman_Float
	Sin256(object.ypos, object.angle)
	object.ypos <<= 10
	object.ypos += object.originPos.y
	object.angle += 2
	object.angle &= 0xFF
end function


private function SYZEggman_CheckAbovePlayer
	if object.playerDetected == false
		temp0 = object[0].xpos
		temp0 >>= 21
		temp1 = object.xpos
		temp1 >>= 21
		if temp0 == temp1
			temp0 = object.xpos
			temp0 >>= 16
			temp0 &= 31
			if temp0 == 16
				object.xpos &= 0xFFFF0000
				object.yvel = 0x18000
				object.flameAnim = FLAME_INACTIVE
				object.state = SYZEGGMAN_LOWERDOWN
				object.playerDetected = true
			end if
		end if
	end if
end function


private function SYZEggman_HandleBlockMovement
	arrayPos0 = object.grabbedBlockSlot
	object[arrayPos0].ypos = object.ypos
	object[arrayPos0].ypos += 0x280000
end function


// ========================
// Events
// ========================

event ObjectUpdate
	if camera[0].ypos < stage.newYBoundary1
		camera[0].ypos += 2
	end if

	switch object.state
	case SYZEGGMAN_AWAITPLAYER
		temp0 = bossMarker[+1].ixpos
		temp0 += screen.xcenter
		stage.newXBoundary2 = temp0

		temp0 -= screen.xsize
		stage.newXBoundary1 = temp0

		temp0 = bossMarker[+1].iypos
		temp0 -= screen.ysize
		stage.newYBoundary1 = temp0
		stage.newYBoundary2 = bossMarker[+1].iypos

		if object[0].xpos > bossMarker[+1].xpos
			object.boundsB = bossMarker[+1].ypos
			object.boundsB -= 0xC80000
			object.originPos.y = bossMarker[+1].ypos
			object.originPos.y -= 0xC80000
			object.boundsL = bossMarker[+1].xpos
			object.boundsL -= 0x960000
			object.boundsR = bossMarker[+1].xpos
			object.boundsR += 0x960000
			if screen.xsize > 320
				bossBarrier[+2].state = BOSSBARRIER_RISE
				bossBarrier[+3].state = BOSSBARRIER_RISE
			end if

			object.xvel = -0x10000
			object.flameAnim = FLAME_ACTIVE
			ResetObjectEntity(SLOT_MUSICEVENT_BOSS, TypeName[Music Event], MUSICEVENT_FADETOBOSS, 0, 0)
			// 26 = SLOT_MUSICEVENT_BOSS
			object[26].priority = PRIORITY_ACTIVE
			object.grabbedBlockSlot = -1
			object.state++
		end if
		break

	case SYZEGGMAN_MOVELEFT
		object.xpos += object.xvel
		if object.xpos <= object.boundsL
			FlipSign(object.xvel)
			object.direction = FACING_LEFT
			object.playerDetected = false
			object.state = SYZEGGMAN_MOVERIGHT
		end if
		CallFunction(SYZEggman_Float)
		CallFunction(SYZEggman_CheckAbovePlayer)
		break

	case SYZEGGMAN_MOVERIGHT
		object.xpos += object.xvel
		if object.xpos >= object.boundsR
			FlipSign(object.xvel)
			object.direction = FACING_RIGHT
			object.playerDetected = false
			object.state = SYZEGGMAN_MOVELEFT
		end if
		CallFunction(SYZEggman_Float)
		CallFunction(SYZEggman_CheckAbovePlayer)
		break

	case SYZEGGMAN_LOWERDOWN
		object.ypos += object.yvel
		temp0 = bossMarker[+1].ypos
		temp0 -= 0x4C0000

		if object.ypos >= temp0
			object.ypos = temp0
			object.yvel = 0
			object.state++
		end if

		if object.stingerPos < 0x200000
			object.stingerPos += 0x20000
		end if
		break

	case SYZEGGMAN_GRABBLOCK
		object.grabbedBlockSlot = -1
		foreach (TypeName[Boss Block], arrayPos0, ACTIVE_ENTITIES)
			BoxCollisionTest(C_TOUCH, object.entityPos, -8, -8, 8, 32, arrayPos0, -16, -16, 16, 16)
			if checkResult == true
				object[arrayPos0].state = SYZEGGMAN_MOVELEFT
				object.grabbedBlockSlot = arrayPos0
			end if
		next

		if object.grabbedBlockSlot > -1
			object.state = SYZEGGMAN_GRABBEDBLOCK
			if object.animation == SYZEGGANI_IDLE
				object.animation = SYZEGGANI_PANIC
				object.animationTimer = 0
			end if
		else
			object.yvel = -0x40000
			object.state = SYZEGGMAN_MISSEDBLOCK
		end if
		break

	case SYZEGGMAN_GRABBEDBLOCK
		object.timer++
		if object.timer == 20
			object.timer = 0
			object.originPos.y = object.ypos
			object.state++
		end if
		break

	case SYZEGGMAN_SHAKEBLOCK
		temp0 = object.timer
		temp0 >>= 1
		temp0 &= 1
		if temp0 == 0
			object.ypos = -0x20000
		else
			object.ypos = 0x20000
		end if

		object.ypos += object.originPos.y
		object.timer++
		if object.timer == 32
			object.timer = 0
			object.yvel = -0x80000
			if object.animation != SYZEGGANI_PANIC
				object.animation = SYZEGGANI_PANIC
				object.animationTimer = 0
			end if
			object.state++
		end if

		CallFunction(SYZEggman_HandleBlockMovement)
		break

	case SYZEGGMAN_RISEUP
		object.ypos += object.yvel
		temp0 = bossMarker[+1].ypos
		temp0 -= 0xF00000
		if object.ypos <= temp0
			object.ypos = temp0
			object.originPos.y = object.ypos
			object.yvel = 0x18000
			object.state++
		end if
		CallFunction(SYZEggman_HandleBlockMovement)
		break

	case SYZEGGMAN_DESTROYBLOCK
		object.timer++
		if object.timer == 48
			object.ypos = object.originPos.y
			CallFunction(SYZEggman_HandleBlockMovement)
			object.timer = 0
			object.animation = SYZEGGANI_IDLE
			object.animationTimer = 0
			object[arrayPos0].type = TypeName[Blank Object]

			CreateTempObject(TypeName[Boss Block], 1, object[arrayPos0].xpos, object[arrayPos0].ypos)
			object[tempObjectPos].ixpos -= 8
			object[tempObjectPos].iypos -= 8
			object[tempObjectPos].xvel = -0x18000
			object[tempObjectPos].yvel = -0x20000

			CreateTempObject(TypeName[Boss Block], 2, object[arrayPos0].xpos, object[arrayPos0].ypos)
			object[tempObjectPos].ixpos += 8
			object[tempObjectPos].iypos -= 8
			object[tempObjectPos].xvel = 0x18000
			object[tempObjectPos].yvel = -0x20000

			CreateTempObject(TypeName[Boss Block], 3, object[arrayPos0].xpos, object[arrayPos0].ypos)
			object[tempObjectPos].ixpos -= 8
			object[tempObjectPos].iypos += 8
			object[tempObjectPos].xvel = -0x10000
			object[tempObjectPos].yvel = -0x10000

			CreateTempObject(TypeName[Boss Block], 4, object[arrayPos0].xpos, object[arrayPos0].ypos)
			object[tempObjectPos].ixpos += 8
			object[tempObjectPos].iypos += 8
			object[tempObjectPos].xvel = 0x10000
			object[tempObjectPos].yvel = -0x10000

			PlaySfx(SfxName[Block Break], false)
			object.grabbedBlockSlot = -1
			object.state++
		else
			object.originPos.y += object.yvel
			if object.originPos.y >= object.boundsB
				object.originPos.y = object.boundsB
				object.yvel = 0
			end if
			temp0 = object.timer
			temp0 >>= 1
			temp0 &= 1
			if temp0 == 0
				object.ypos = -0x10000
			else
				object.ypos = 0x10000
			end if
			object.ypos += object.originPos.y
			CallFunction(SYZEggman_HandleBlockMovement)
		end if
		break

	case SYZEGGMAN_RETRACTSTINGER
		if object.stingerPos > 0
			object.stingerPos -= 0x20000
		else
			object.angle = 0
			object.flameAnim = FLAME_ACTIVE
			if object.xvel > 0
				object.state = SYZEGGMAN_MOVERIGHT
			else
				object.state = SYZEGGMAN_MOVELEFT
			end if
		end if
		break

	case SYZEGGMAN_MISSEDBLOCK
		object.ypos += object.yvel
		object.yvel += 0x800
		if object.ypos <= object.boundsB
			object.ypos = object.boundsB
			object.originPos.y = object.ypos
			object.state = SYZEGGMAN_RETRACTSTINGER
		end if
		break

	case SYZEGGMAN_EXPLODE
		if object.stingerPos > 0
			object.stingerPos -= 0x20000
		end if

		object.timer++
		temp0 = object.timer
		temp0 &= 7
		if temp0 == 4
			Rand(temp0, 64)
			temp0 -= 32
			temp0 <<= 16
			temp0 += object.xpos
			Rand(temp1, 48)
			temp1 -= 24
			temp1 <<= 16
			temp1 += object.ypos
			CreateTempObject(TypeName[Explosion], 0, temp0, temp1)
			PlaySfx(SfxName[Explosion], false)
		end if

		if object.timer == 180
			object.animation = SYZEGGANI_TOASTED
			object.animationTimer = 0
			object.timer = 0
			object.direction = FACING_LEFT
			ResetObjectEntity(SLOT_MUSICEVENT_BOSS, TypeName[Music Event], MUSICEVENT_FADETOSTAGE, 0, 0)
			// 26 = SLOT_MUSICEVENT_BOSS
			object[26].priority = PRIORITY_ACTIVE
			object.state++
		end if
		break

	case SYZEGGMAN_DEFEATED
		if object.timer < 40
			object.ypos += object.yvel
			object.yvel -= 0x800
			object.timer++
		else
			object.timer = 0
			object.yvel = 0
			object.angle = 0
			object.originPos.y = object.ypos
			object.state++
		end if
		break

	case SYZEGGMAN_FLEE
		CallFunction(SYZEggman_Float)
		if object.timer < 8
			object.timer++
		else
			object.timer = 0
			object.animation = SYZEGGANI_PANIC
			object.animationTimer = 0
			object.flameAnim = FLAME_EXPLODE
			object.flameAnimTimer = 0
			object.priority = PRIORITY_ACTIVE
			temp0 = tileLayer[0].xsize
			temp0 <<= 7
			stage.newXBoundary2 = temp0
			object.state++
		end if
		break

	case SYZEGGMAN_ESCAPE
		CallFunction(SYZEggman_Float)
		object.xpos += 0x40000
		object.originPos.y -= 0x4000
		if object.outOfBounds == true
			object.type = TypeName[Blank Object]
			object.priority = PRIORITY_BOUNDS
		end if
		break
		
	end switch

	if object.health != 0
		if object.invincibilityTimer > 0
			object.invincibilityTimer--
		end if

		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			if object.invincibilityTimer == 0
				CheckEqual(player[currentPlayer].animation, ANI_HURT)
				temp0 = checkResult
				CheckEqual(player[currentPlayer].animation, ANI_DYING)
				temp0 |= checkResult
				CheckEqual(player[currentPlayer].animation, ANI_DROWNING)
				temp0 |= checkResult
				if temp0 != 0
					if object.animation != SYZEGGANI_LAUGH
						object.animation = SYZEGGANI_LAUGH
						object.animationTimer = 0
						object.eggmanFrame = 12
					end if
				end if

				if object.stingerPos > 0x100000
					BoxCollisionTest(C_TOUCH, object.entityPos, -6, 0, 6, 40, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
					if checkResult == true
						CallFunction(Player_Hit)
					end if
				end if

				BoxCollisionTest(C_TOUCH, object.entityPos, -20, -16, 20, 16, currentPlayer, player[currentPlayer].hitboxLeft, player[currentPlayer].hitboxTop, player[currentPlayer].hitboxRight, player[currentPlayer].hitboxBottom)
				if checkResult == true
					CallFunction(Player_CheckHit)
					if checkResult == true
						object.health--
						if object.health == 0
#platform: USE_ORIGINS
							CallNativeFunction2(NotifyCallback, NOTIFY_KILL_BOSS, 0)
							CallNativeFunction2(NotifyCallback, NOTIFY_BOSS_END, true)
#endplatform
							player.score += 1000
							object.animation = SYZEGGANI_DEFEATED
							object.animationTimer = 0
							object.flameAnim = FLAME_INACTIVE
							object.timer = 0
							object.yvel = 0
							object.state = SYZEGGMAN_EXPLODE
							if screen.xsize > 320
								bossBarrier[+2].state = BOSSBARRIER_LOWER
								bossBarrier[+3].state = BOSSBARRIER_LOWER
							end if

							if object.grabbedBlockSlot > -1
								arrayPos0 = object.grabbedBlockSlot
								object[arrayPos0].type = TypeName[Blank Object]

								CreateTempObject(TypeName[Boss Block], 1, object[arrayPos0].xpos, object[arrayPos0].ypos)
								object[tempObjectPos].ixpos -= 8
								object[tempObjectPos].iypos -= 8
								object[tempObjectPos].xvel = -0x18000
								object[tempObjectPos].yvel = -0x20000

								CreateTempObject(TypeName[Boss Block], 2, object[arrayPos0].xpos, object[arrayPos0].ypos)
								object[tempObjectPos].ixpos += 8
								object[tempObjectPos].iypos -= 8
								object[tempObjectPos].xvel = 0x18000
								object[tempObjectPos].yvel = -0x20000

								CreateTempObject(TypeName[Boss Block], 3, object[arrayPos0].xpos, object[arrayPos0].ypos)
								object[tempObjectPos].ixpos -= 8
								object[tempObjectPos].iypos += 8
								object[tempObjectPos].xvel = -0x10000
								object[tempObjectPos].yvel = -0x10000

								CreateTempObject(TypeName[Boss Block], 4, object[arrayPos0].xpos, object[arrayPos0].ypos)
								object[tempObjectPos].ixpos += 8
								object[tempObjectPos].iypos += 8
								object[tempObjectPos].xvel = 0x10000
								object[tempObjectPos].yvel = -0x10000
							end if
						else
							object.animation = SYZEGGANI_HIT
							object.animationTimer = 0
							object.invincibilityTimer = 32
							PlaySfx(SfxName[Boss Hit], false)
						end if
					end if
				end if
			end if
		next
	end if

	// Screen R Boundary Lock
	if object.state < SYZEGGMAN_FLEE
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			temp0 = player[currentPlayer].collisionRight
			temp0 <<= 16
			temp0 += player[currentPlayer].xpos
			temp1 = stage.curXBoundary2
			temp1 <<= 16
			if temp0 > temp1
				player[currentPlayer].xvel = 0
				player[currentPlayer].speed = 0
				player[currentPlayer].xpos = temp1
				temp0 = player[currentPlayer].collisionRight
				temp0 <<= 16
				player[currentPlayer].xpos -= temp0
			end if
		next
	end if
end event


event ObjectDraw

	temp0 = object.ypos
	temp0 += object.stingerPos
	DrawSpriteFX(212, FX_FLIP, object.xpos, temp0)

	switch object.animation
	default
	case SYZEGGANI_INVISIBLE
		break

	case SYZEGGANI_IDLE
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 3
			object.eggmanFrame = 3
		end if
		if object.eggmanFrame > 11
			object.eggmanFrame = 3
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		break

	case SYZEGGANI_LAUGH
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 12
			object.eggmanFrame = 12
		end if
		if object.eggmanFrame > 108
			object.animation = SYZEGGANI_IDLE
		end if
		if object.eggmanFrame > 107
			if object[0].animation != ANI_DYING
				object.animation = SYZEGGANI_IDLE
			end if
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		break

	case SYZEGGANI_LAUGH_LOOP
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 12
			object.eggmanFrame = 12
		end if
		if object.eggmanFrame > 102
			object.eggmanFrame = 43
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		break

	case SYZEGGANI_HIT
		object.equipmentAnimTimer++
		object.animationTimer++
		object.eggmanFrame++
		object.equipmentFrame++
		object.mobileAnimTimer++
		object.flameAnim = FLAME_ACTIVE
		if object.mobileAnimTimer > 1
			object.frame++
			object.mobileAnimTimer = 0
		end if
		if object.frame < 0
			object.frame = 0
		end if
		if object.frame > 1
			object.frame = 0
		end if
		if object.equipmentFrame < 212
			object.equipmentFrame = 212
		end if
		if object.equipmentFrame > 213
			object.equipmentFrame = 212
		end if
		if object.eggmanFrame < 109
			object.eggmanFrame = 109
		end if
		if object.eggmanFrame > 139
			object.eggmanFrame = 109
		end if
		if object.animationTimer == 30
			object.animationTimer = 0
			object.animation = SYZEGGANI_IDLE
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.frame, FX_FLIP, object.xpos, object.ypos)
		break

	case SYZEGGANI_DEFEATED
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 140
			object.eggmanFrame = 140
		end if
		if object.eggmanFrame > 175
			object.eggmanFrame = 140
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		break
		
	case SYZEGGANI_TOASTED
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 140
			object.eggmanFrame = 140
		end if
		if object.eggmanFrame > 175
			object.eggmanFrame = 140
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		break

	case SYZEGGANI_PANIC
		object.animationTimer++
		object.eggmanFrame++
		if object.eggmanFrame < 176
			object.eggmanFrame = 176
		end if
		if object.eggmanFrame > 193
			object.eggmanFrame = 176
		end if
		DrawSpriteFX(2, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(object.eggmanFrame, FX_FLIP, object.xpos, object.ypos)
		DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
		break
		
	end switch

	switch object.flameAnim
	default
	case FLAME_INACTIVE
		break

	case FLAME_ACTIVE
		object.flameAnimTimer++
		if object.flameAnimTimer > 1
			object.flameFrame++
			object.flameAnimTimer = 0
		end if
		if object.flameFrame < 194
			object.flameFrame = 194
		end if
		if object.flameFrame > 198
			object.flameFrame = 194
		end if
		DrawSpriteFX(object.flameFrame, FX_FLIP, object.xpos, object.ypos)
		break

	case FLAME_EXPLODE
		object.flameAnimTimer++
		if object.flameAnimTimer > 1
			object.flameFrame++
			object.flameAnimTimer = 0
		end if
		if object.flameFrame < 199
			object.flameFrame = 199
		end if
		if object.flameFrame > 211
			object.flameFrame = 207
		end if
		DrawSpriteFX(object.flameFrame, FX_FLIP, object.xpos, object.ypos)
		break
		
	end switch
end event


event ObjectStartup
	switch stage.playerListPos
	case PLAYER_SONIC_A
		LoadSpriteSheet("Global/Eggman.gif")
		break

	case PLAYER_TAILS_A
		LoadSpriteSheet("Global/Eggman.gif")
		break

	case PLAYER_KNUCKLES_A
		LoadSpriteSheet("Global/EggRobo.gif")
		break

	case PLAYER_AMY_A
		LoadSpriteSheet("Global/Eggman.gif")
		break
	end switch
	// Empty Egg Mobile
	SpriteFrame(-28, -32, 64, 56, 128, 174) // Frame 0
	SpriteFrame(-28, -32, 64, 56, 131, 1) // Frame 1

	// Egg Mobile Seat
	SpriteFrame(-28, -32, 64, 24, 64, 232) // Frame 2

	// Eggman Idle
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 3
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 4
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 5
	SpriteFrame(-28, -32, 64, 24, 1, 25) // Frame 6
	SpriteFrame(-28, -32, 64, 24, 1, 25) // Frame 7
	SpriteFrame(-28, -32, 64, 24, 1, 25) // Frame 8
	SpriteFrame(-28, -32, 64, 24, 1, 50) // Frame 9
	SpriteFrame(-28, -32, 64, 24, 1, 50) // Frame 10
	SpriteFrame(-28, -32, 64, 24, 1, 50) // Frame 11

	//Eggman Laugh
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 12
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 13

	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 14
	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 15
	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 16
	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 17

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 18
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 19
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 20
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 21

	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 22
	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 23
	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 24
	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 25

	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 26
	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 27
	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 28
	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 29

	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 30
	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 31
	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 32
	SpriteFrame(-28, -32, 64, 24, 1, 150) // Frame 33

	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 34
	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 35
	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 36
	SpriteFrame(-28, -32, 64, 24, 1, 125) // Frame 37

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 38
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 39
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 40
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 41
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 42

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 43
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 44
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 45
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 46
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 47

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 48
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 49
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 50
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 51
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 52

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 53
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 54
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 55
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 56
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 57

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 58
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 59
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 60
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 61
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 62

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 63
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 64
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 65
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 66
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 67

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 68
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 69
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 70
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 71
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 72

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 73
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 74
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 75
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 76
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 77

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 78
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 79
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 80
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 81
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 82

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 83
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 84
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 85
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 86
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 87

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 88
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 89
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 90
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 91
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 92

	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 93
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 94
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 95
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 96
	SpriteFrame(-28, -32, 64, 24, 1, 100) // Frame 97

	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 98
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 99
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 10
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 101
	SpriteFrame(-28, -32, 64, 24, 66, 0) // Frame 102

	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 103
	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 104
	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 105
	SpriteFrame(-28, -32, 64, 24, 1, 75) // Frame 106

	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 107
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 108

	// Eggman Hurt
	SpriteFrame(-28, -32, 64, 24, 66, 25) // Frame 109
	SpriteFrame(-28, -32, 64, 24, 66, 25) // Frame 110

	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 111
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 112

	SpriteFrame(-28, -32, 64, 24, 66, 75) // Frame 113
	SpriteFrame(-28, -32, 64, 24, 66, 75) // Frame 114

	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 115
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 116

	SpriteFrame(-28, -32, 64, 24, 66, 100) // Frame 117
	SpriteFrame(-28, -32, 64, 24, 66, 100) // Frame 118

	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 119
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 120

	SpriteFrame(-28, -32, 64, 24, 66, 125) // Frame 121
	SpriteFrame(-28, -32, 64, 24, 66, 125) // Frame 122

	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 123
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 124
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 125

	SpriteFrame(-28, -32, 64, 24, 66, 150) // Frame 126
	SpriteFrame(-28, -32, 64, 24, 66, 150) // Frame 127
	SpriteFrame(-28, -32, 64, 24, 66, 150) // Frame 128

	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 129
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 130
	SpriteFrame(-28, -32, 64, 24, 66, 50) // Frame 131

	SpriteFrame(-28, -32, 64, 24, 64, 175) // Frame 132
	SpriteFrame(-28, -32, 64, 24, 64, 175) // Frame 133
	SpriteFrame(-28, -32, 64, 24, 64, 175) // Frame 134
	SpriteFrame(-28, -32, 64, 24, 64, 175) // Frame 135
	SpriteFrame(-28, -32, 64, 24, 64, 175) // Frame 136
	SpriteFrame(-28, -32, 64, 24, 64, 175) // Frame 137

	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 138
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 139

	// Eggman Toasted
	SpriteFrame(-28, -32, 64, 24, 64, 200) // Frame 140
	SpriteFrame(-28, -32, 64, 24, 64, 200) // Frame 141
	SpriteFrame(-28, -32, 64, 24, 64, 200) // Frame 142
	SpriteFrame(-28, -32, 64, 24, 64, 200) // Frame 143
	SpriteFrame(-28, -32, 64, 24, 64, 200) // Frame 144
	SpriteFrame(-28, -32, 64, 24, 64, 200) // Frame 145
	SpriteFrame(-28, -32, 64, 24, 131, 58) // Frame 146
	SpriteFrame(-28, -32, 64, 24, 131, 58) // Frame 147
	SpriteFrame(-28, -32, 64, 24, 131, 58) // Frame 148
	SpriteFrame(-28, -32, 64, 24, 131, 58) // Frame 149
	SpriteFrame(-28, -32, 64, 24, 131, 58) // Frame 150
	SpriteFrame(-28, -32, 64, 24, 131, 58) // Frame 151
	SpriteFrame(-28, -32, 64, 24, 131, 83) // Frame 152
	SpriteFrame(-28, -32, 64, 24, 131, 83) // Frame 153
	SpriteFrame(-28, -32, 64, 24, 131, 83) // Frame 154
	SpriteFrame(-28, -32, 64, 24, 131, 83) // Frame 155
	SpriteFrame(-28, -32, 64, 24, 131, 83) // Frame 156
	SpriteFrame(-28, -32, 64, 24, 131, 83) // Frame 157
	SpriteFrame(-28, -32, 64, 24, 130, 151) // Frame 158
	SpriteFrame(-28, -32, 64, 24, 130, 151) // Frame 159
	SpriteFrame(-28, -32, 64, 24, 130, 151) // Frame 160
	SpriteFrame(-28, -32, 64, 24, 130, 151) // Frame 161
	SpriteFrame(-28, -32, 64, 24, 130, 151) // Frame 162
	SpriteFrame(-28, -32, 64, 24, 130, 151) // Frame 163
	SpriteFrame(-28, -32, 64, 24, 255, 1) // Frame 164
	SpriteFrame(-28, -32, 64, 24, 255, 1) // Frame 165
	SpriteFrame(-28, -32, 64, 24, 255, 1) // Frame 166
	SpriteFrame(-28, -32, 64, 24, 255, 1) // Frame 167
	SpriteFrame(-28, -32, 64, 24, 255, 1) // Frame 168
	SpriteFrame(-28, -32, 64, 24, 255, 1) // Frame 169
	SpriteFrame(-28, -32, 64, 24, 255, 26) // Frame 170
	SpriteFrame(-28, -32, 64, 24, 255, 26) // Frame 171
	SpriteFrame(-28, -32, 64, 24, 255, 26) // Frame 172
	SpriteFrame(-28, -32, 64, 24, 255, 26) // Frame 173
	SpriteFrame(-28, -32, 64, 24, 255, 26) // Frame 174
	SpriteFrame(-28, -32, 64, 24, 255, 26) // Frame 175

	// Eggman Sweat
	SpriteFrame(-28, -32, 64, 24, 255, 51) // Frame 176
	SpriteFrame(-28, -32, 64, 24, 255, 51) // Frame 177
	SpriteFrame(-28, -32, 64, 24, 255, 51) // Frame 178
	SpriteFrame(-28, -32, 64, 24, 255, 76) // Frame 179
	SpriteFrame(-28, -32, 64, 24, 255, 76) // Frame 180
	SpriteFrame(-28, -32, 64, 24, 255, 76) // Frame 181
	SpriteFrame(-28, -32, 64, 24, 255, 101) // Frame 182
	SpriteFrame(-28, -32, 64, 24, 255, 101) // Frame 183
	SpriteFrame(-28, -32, 64, 24, 255, 101) // Frame 184
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 185
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 186
	SpriteFrame(-28, -32, 64, 24, 1, 0) // Frame 187
	SpriteFrame(-28, -32, 64, 24, 1, 25) // Frame 188
	SpriteFrame(-28, -32, 64, 24, 1, 25) // Frame 189
	SpriteFrame(-28, -32, 64, 24, 1, 25) // Frame 190
	SpriteFrame(-28, -32, 64, 24, 1, 50) // Frame 191
	SpriteFrame(-28, -32, 64, 24, 1, 50) // Frame 192
	SpriteFrame(-28, -32, 64, 24, 1, 50) // Frame 193

	// Jet
	SpriteFrame(34, 1, 10, 9, 196, 2) // Frame 194
	SpriteFrame(33, 0, 15, 11, 196, 12) // Frame 195
	SpriteFrame(33, 1, 10, 9, 196, 52) // Frame 196
	SpriteFrame(33, 1, 13, 9, 196, 105) // Frame 197
	SpriteFrame(33, 0, 25, 11, 210, 104) // Frame 198

	// Jet Burst
	SpriteFrame(34, -1, 22, 14, 213, 1) // Frame 199
	SpriteFrame(34, -1, 22, 14, 213, 1) // Frame 200
	SpriteFrame(34, -8, 32, 26, 213, 16) // Frame 201
	SpriteFrame(34, -8, 32, 26, 213, 16) // Frame 202
	SpriteFrame(34, -8, 32, 26, 205, 169) // Frame 203
	SpriteFrame(34, -8, 32, 26, 205, 169) // Frame 204
	SpriteFrame(34, -8, 32, 26, 205, 196) // Frame 205
	SpriteFrame(34, -1, 22, 14, 213, 1) // Frame 206
	SpriteFrame(34, 1, 10, 9, 196, 2) // Frame 207
	SpriteFrame(33, 0, 15, 11, 196, 12) // Frame 208
	SpriteFrame(33, 1, 10, 9, 196, 52) // Frame 209
	SpriteFrame(33, 1, 13, 9, 196, 105) // Frame 210
	SpriteFrame(33, 0, 25, 11, 210, 104) // Frame 211

	// Spike
	SpriteFrame(-8, -16, 16, 32, 238, 60) // Frame 212
	SpriteFrame(-8, -16, 16, 32, 238, 152) // Frame 213

	foreach (TypeName[Eggman], arrayPos0, ALL_ENTITIES)
		object[arrayPos0].animation = SYZEGGANI_IDLE
#platform: USE_ORIGINS
		if game.bossOneLife == false
			object[arrayPos0].health = 8
		else
			object[arrayPos0].health = 1
		end if
#endplatform
#platform: USE_STANDALONE
		object[arrayPos0].health = 8
#endplatform
	next
end event


event RSDKDraw
	temp0 = object.ypos
	temp0 += 0x200000
	DrawSpriteFX(19, FX_FLIP, object.xpos, temp0)
	
	DrawSpriteFX(0, FX_FLIP, object.xpos, object.ypos)
	
	if ditor.showGizmos == true
		editor.drawingOverlay = true

		DrawArrow(object.xpos, object.ypos, object[+1].xpos, object[+1].ypos, 255, 255, 0) // arena marker
		DrawArrow(object.xpos, object.ypos, object[+2].xpos, object[+2].ypos, 255, 255, 0) // boundsL
		DrawArrow(object.xpos, object.ypos, object[+3].xpos, object[+3].ypos, 255, 255, 0) // boundsR

		editor.drawingOverlay = false
	end if
end event


event RSDKLoad
	LoadSpriteSheet("Global/Eggman.gif")
	SpriteFrame(-28, -32, 64, 56, 1, 1)
	SpriteFrame(-28, -32, 64, 56, 66, 1)
	SpriteFrame(-28, -32, 64, 56, 131, 1)
	SpriteFrame(-28, -32, 64, 56, 1, 58)
	SpriteFrame(-28, -32, 64, 56, 1, 1)
	SpriteFrame(-28, -32, 64, 56, 1, 58)
	SpriteFrame(-28, -32, 64, 56, 66, 58)
	SpriteFrame(-28, -32, 64, 56, 131, 58)
	SpriteFrame(-28, -32, 64, 56, 66, 1)
	SpriteFrame(-28, -32, 64, 56, 1, 115)
	SpriteFrame(-28, -32, 64, 56, 66, 115)
	SpriteFrame(34, 1, 10, 9, 196, 2)
	SpriteFrame(33, 0, 15, 11, 196, 12)
	SpriteFrame(34, -1, 22, 14, 213, 1)
	SpriteFrame(34, -8, 32, 26, 213, 16)
	SpriteFrame(34, -1, 22, 14, 213, 1)
	SpriteFrame(34, -8, 32, 26, 213, 16)
	SpriteFrame(34, 1, 10, 9, 196, 2)
	SpriteFrame(33, 0, 15, 11, 196, 12)
	SpriteFrame(-8, -16, 16, 32, 238, 60)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
