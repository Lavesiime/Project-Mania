// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Newtron Shot Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias object.value0 : object.timer


// ========================
// Events
// ========================

event ObjectUpdate
	object.xpos += object.xvel

	object.timer++
	if object.timer >= 2
		object.frame++
		object.timer = 0
	end if
	if object.frame > 5
		object.frame = 0
	end if

	if object.outOfBounds == true
		object.type = TypeName[Blank Object]
	end if

	foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
		BoxCollisionTest(C_TOUCH, object.entityPos, -6, -6, 6, 6, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
		if checkResult == true
			CallFunction(Player_ProjectileHit)
		end if
	next
end event


event ObjectDraw
	object.inkEffect = INK_ADD
	object.alpha = 180
	DrawSpriteFX(object.frame, FX_INK, object.xpos, object.ypos)
end event


event ObjectStartup
	CheckCurrentStageFolder("Zone01")
	if checkResult == true
		LoadSpriteSheet("GHZ/Objects.gif")
		SpriteFrame(-6, -6, 12, 12, 175, 105)
		SpriteFrame(-6, -6, 12, 12, 165, 118)
		SpriteFrame(-6, -6, 12, 12, 178, 118)
		SpriteFrame(-6, -6, 12, 12, 165, 131)
		SpriteFrame(-6, -6, 12, 12, 149, 105)
		SpriteFrame(-6, -6, 12, 12, 162, 105)
	end if

	CheckCurrentStageFolder("Zone07")
	if checkResult == true
		// Check if in MBZ and then... load GHZ sprites?
		// Likely an error, the given numbers match up with the MBZ sheet
		LoadSpriteSheet("GHZ/Objects.gif")
		SpriteFrame(-8, -8, 16, 16, 35, 51)
		SpriteFrame(-8, -8, 16, 16, 52, 51)
	end if
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	CheckCurrentStageFolder("Zone07")
	if checkResult == true
		// Decomp note - using the proper sheet here instead of the GHZ one
		LoadSpriteSheet("MBZ/Objects.gif")
		SpriteFrame(-8, -8, 16, 16, 35, 51)
	else
		LoadSpriteSheet("GHZ/Objects.gif")
		SpriteFrame(-6, -6, 12, 12, 160, 111)
	end if
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
