// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Break Wall Object
// ----------------------------------

// ========================
// Aliases
// ========================

private alias 0 : BREAKWALL_WALL
private alias 1 : BREAKWALL_DEBRIS

// Player Aliases
private alias object.yvel : player.yvel
private alias object.gravity : player.gravity
private alias object.animation : player.animation
private alias object.value19 : player.badnikBonus


// ========================
// Function Declarations
// ========================

reserve function BreakWall_DebugDraw
reserve function BreakWall_DebugSpawn


// ========================
// Function Definitions
// ========================

private function BreakWall_DebugDraw
	DrawSprite(0)
end function


private function BreakWall_DebugSpawn
	CreateTempObject(TypeName[Break Wall], 0, object.xpos, object.ypos)
end function


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case BREAKWALL_WALL
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			BoxCollisionTest(C_TOUCH, object.entityPos, -24, -40, 24, 40, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
			temp1 = checkResult
			if checkResult == true

				temp0 = 0
				
				while temp0 < 5
					// y-offset
					temp2 = 32
					temp3 = 16
					temp3 *= temp0
					temp2 -= temp3
					// frame
					temp3 = 1
					temp5 = temp0
					temp5 *= 3
					temp3 += temp5
					// yvel, multiplied by 1/1/9 per piece
					temp4 = -0x40000
					temp5 = 0x20000
					temp5 *= temp0
					temp4 += temp5
					// xvel, increases by 0x10000 per piece
					temp5 = -0x30000
					
					CreateTempObject(TypeName[Break Wall], 0, object.xpos, object.ypos)
					object[tempObjectPos].ixpos -= 16
					object[tempObjectPos].iypos -= temp2
					object[tempObjectPos].xvel = temp5
					object[tempObjectPos].yvel = temp4
					object[tempObjectPos].state = BREAKWALL_DEBRIS
					object[tempObjectPos].drawOrder = 5
					object[tempObjectPos].frame = temp3
					temp3++
					temp4 *= 10
					temp4 /= 9
					temp5 += 0x10000

					CreateTempObject(TypeName[Break Wall], 0, object.xpos, object.ypos)
					object[tempObjectPos].ixpos -= 0
					object[tempObjectPos].iypos -= temp2
					object[tempObjectPos].xvel = temp5
					object[tempObjectPos].yvel = temp4
					object[tempObjectPos].state = BREAKWALL_DEBRIS
					object[tempObjectPos].drawOrder = 5
					object[tempObjectPos].frame = temp3
					temp3++
					temp4 *= 10
					temp4 /= 9
					temp5 += 0x10000

					CreateTempObject(TypeName[Break Wall], 0, object.xpos, object.ypos)
					object[tempObjectPos].ixpos -= -16
					object[tempObjectPos].iypos -= temp2
					object[tempObjectPos].xvel = temp5
					object[tempObjectPos].yvel = temp4
					object[tempObjectPos].state = BREAKWALL_DEBRIS
					object[tempObjectPos].drawOrder = 5
					object[tempObjectPos].frame = temp3
					
					temp0++
				loop

				object.type = TypeName[Blank Object]
				PlaySfx(SfxName[Block Break], false)
			end if
		next
		break

	case BREAKWALL_DEBRIS
		object.xpos += object.xvel
		object.ypos += object.yvel
		object.yvel += 0x3800
		if object.outOfBounds == true
			object.type = TypeName[Blank Object]
		end if
		break
		
	end switch
end event


event ObjectDraw
	DrawSprite(object.frame)
end event


event ObjectStartup
	LoadSpriteSheet("LZ/Objects2.gif")
	SpriteFrame(-24, -40, 48, 80, 207, 1)
	SpriteFrame(-8, -8, 16, 16, 207, 1)
	SpriteFrame(-8, -8, 16, 16, 223, 1)
	SpriteFrame(-8, -8, 16, 16, 239, 1)
	SpriteFrame(-8, -8, 16, 16, 207, 17)
	SpriteFrame(-8, -8, 16, 16, 223, 17)
	SpriteFrame(-8, -8, 16, 16, 239, 17)
	SpriteFrame(-8, -8, 16, 16, 207, 33)
	SpriteFrame(-8, -8, 16, 16, 223, 33)
	SpriteFrame(-8, -8, 16, 16, 239, 33)
	SpriteFrame(-8, -8, 16, 16, 207, 49)
	SpriteFrame(-8, -8, 16, 16, 223, 49)
	SpriteFrame(-8, -8, 16, 16, 239, 49)
	SpriteFrame(-8, -8, 16, 16, 207, 65)
	SpriteFrame(-8, -8, 16, 16, 223, 65)
	SpriteFrame(-8, -8, 16, 16, 239, 65)

	SetTableValue(TypeName[Break Wall], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(BreakWall_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(BreakWall_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("MZ/Objects.gif")
	SpriteFrame(-16, -16, 32, 32, 317, 363)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
