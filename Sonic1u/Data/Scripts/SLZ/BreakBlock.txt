// ----------------------------------
// RSDK Project: Sonic 1
// Script Description: Break Block Object
// Script Author: Christian Whitehead/Simon Thomley
// Unpacked by Rubberduckycooly's script unpacker
// ----------------------------------

// ========================
// Aliases
// ========================

private alias 0 : BREAKBLOCK_BLOCK
private alias 1 : BREAKBLOCK_DEBRIS

// Player Aliases
private alias object.yvel : player.yvel
private alias object.gravity : player.gravity
private alias object.animation : player.animation
private alias object.value19 : player.badnikBonus

// Achievement Aliases
private alias 3 : ACHIEVEMENT_BLOCKBUSTER


// ========================
// Function Declarations
// ========================

reserve function BreakBlock_DebugDraw
reserve function BreakBlock_DebugSpawn


// ========================
// Function Definitions
// ========================

private function BreakBlock_DebugDraw
	DrawSprite(0)
end function


private function BreakBlock_DebugSpawn
	CreateTempObject(TypeName[Break Block], 0, object.xpos, object.ypos)
end function


// ========================
// Events
// ========================

event ObjectUpdate
	switch object.state
	case BREAKBLOCK_BLOCK
		foreach (GROUP_PLAYERS, currentPlayer, ACTIVE_ENTITIES)
			BoxCollisionTest(C_TOUCH, object.entityPos, -16, -16, 16, 16, currentPlayer, C_BOX, C_BOX, C_BOX, C_BOX)
			temp1 = checkResult
			if checkResult == true

				CreateTempObject(TypeName[Break Block], 0, object.xpos, object.ypos)
				object[tempObjectPos].ixpos -= 8
				object[tempObjectPos].iypos -= 8
				object[tempObjectPos].xvel = -0x20000
				object[tempObjectPos].yvel = -0x20000
				object[tempObjectPos].state = BREAKBLOCK_DEBRIS
				object[tempObjectPos].drawOrder = 5
				object[tempObjectPos].frame = 1

				CreateTempObject(TypeName[Break Block], 0, object.xpos, object.ypos)
				object[tempObjectPos].ixpos += 8
				object[tempObjectPos].iypos -= 8
				object[tempObjectPos].xvel = 0x20000
				object[tempObjectPos].yvel = -0x20000
				object[tempObjectPos].state = BREAKBLOCK_DEBRIS
				object[tempObjectPos].drawOrder = 5
				object[tempObjectPos].frame = 2

				CreateTempObject(TypeName[Break Block], 0, object.xpos, object.ypos)
				object[tempObjectPos].ixpos -= 8
				object[tempObjectPos].iypos += 8
				object[tempObjectPos].xvel = -0x10000
				object[tempObjectPos].yvel = -0x10000
				object[tempObjectPos].state = BREAKBLOCK_DEBRIS
				object[tempObjectPos].drawOrder = 5
				object[tempObjectPos].frame = 3

				CreateTempObject(TypeName[Break Block], 0, object.xpos, object.ypos)
				object[tempObjectPos].ixpos += 8
				object[tempObjectPos].iypos += 8
				object[tempObjectPos].xvel = 0x10000
				object[tempObjectPos].yvel = -0x10000
				object[tempObjectPos].state = BREAKBLOCK_DEBRIS
				object[tempObjectPos].drawOrder = 5
				object[tempObjectPos].frame = 4

				object.type = TypeName[Blank Object]

				PlaySfx(SfxName[Block Break], false)
			end if
		next
		break

	case BREAKBLOCK_DEBRIS
		object.xpos += object.xvel
		object.ypos += object.yvel
		object.yvel += 0x3800
		if object.outOfBounds == true
			object.type = TypeName[Blank Object]
		end if
		break
		
	end switch
end event


event ObjectDraw
	DrawSprite(object.frame)
end event


event ObjectStartup
	LoadSpriteSheet("SLZ/Objects3.gif")
	SpriteFrame(-16, -16, 32, 32, 76, 129)
	SpriteFrame(-8, -8, 16, 16, 76, 129)
	SpriteFrame(-8, -8, 16, 16, 92, 129)
	SpriteFrame(-8, -8, 16, 16, 76, 145)
	SpriteFrame(-8, -8, 16, 16, 92, 145)

	SetTableValue(TypeName[Break Block], DebugMode_ObjCount, DebugMode_TypesTable)
	SetTableValue(BreakBlock_DebugDraw, DebugMode_ObjCount, DebugMode_DrawTable)
	SetTableValue(BreakBlock_DebugSpawn, DebugMode_ObjCount, DebugMode_SpawnTable)
	DebugMode_ObjCount++
end event


// ========================
// Editor Events
// ========================

event RSDKDraw
	DrawSprite(0)
end event


event RSDKLoad
	LoadSpriteSheet("MZ/Objects.gif")
	SpriteFrame(-16, -16, 32, 32, 317, 363)
	
	SetVariableAlias(ALIAS_VAR_PROPVAL, "unused")
end event
